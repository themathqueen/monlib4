/-
Copyright (c) 2023 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
import Mathlib.Analysis.InnerProductSpace.Adjoint
import Monlib.LinearAlgebra.MyIps.Basic
import Monlib.LinearAlgebra.IsProj'

#align_import linear_algebra.my_ips.rank_one

/-!

# rank one operators

This defines the rank one operator $| x \rangle\!\langle y |$ for continuous linear maps
  (see `rank_one`) and linear maps (see `rank_one_lm`).

-/


section rankOne

variable {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]

-- local notation "L(" x ")" => x â†’L[â„‚] x

local notation "âŸª" x "," y "âŸ«_ğ•œ" => @inner ğ•œ _ _ x y

/-- we define the rank one operator $| x \rangle\!\langle y |$ by
  $x \mapsto \langle y,  z \rangle \cdot  x$ -/
def rankOne (x y : E) : E â†’L[ğ•œ] E where
  toFun z := âŸªy,zâŸ«_ğ•œ â€¢ x
  map_add' u v := by simp_rw [inner_add_right, add_smul]
  map_smul' r u := by simp_rw [inner_smul_right, RingHom.id_apply, smul_smul]
  cont := Continuous.smul (Continuous.inner continuous_const continuous_id') (continuous_const)

-- local notation "|" x "âŸ©âŸ¨" y "|" => rankOne x y

@[simp]
theorem rankOne_apply {x y : E} (z : E) : (rankOne x y : E â†’L[ğ•œ] E) z = âŸªy,zâŸ«_ğ•œ â€¢ x :=
  rfl

open ContinuousLinearMap

/-- $| x \rangle\!\langle \alpha\cdot y | = \bar{\alpha} \cdot | x \rangle\!\langle y |$ -/
@[simp]
theorem rankOne.smul_apply {x y : E} {Î± : ğ•œ} :
    rankOne x (Î± â€¢ y) = (starRingEnd ğ•œ) Î± â€¢ (rankOne x y : E â†’L[ğ•œ] E) :=
  by
  ext
  simp_rw [ContinuousLinearMap.smul_apply, rankOne_apply, inner_smul_left, smul_smul]

/-- $| \alpha \cdot x \rangle\!\langle y | = \alpha \cdot | x \rangle\!\langle y |$ -/
@[simp]
theorem rankOne.apply_smul {x y : E} {Î± : ğ•œ} : rankOne (Î± â€¢ x) y = Î± â€¢ (rankOne x y : E â†’L[ğ•œ] E) :=
  by
  ext
  simp_rw [ContinuousLinearMap.smul_apply, rankOne_apply, smul_smul, mul_comm]

@[simp]
theorem rankOne.smul_real_apply {x y : E} {Î± : â„} :
    rankOne x ((Î± : ğ•œ) â€¢ y) = (Î± : ğ•œ) â€¢ (rankOne x y : E â†’L[ğ•œ] E) := by
  simp_rw [rankOne.smul_apply, IsROrC.conj_ofReal]

/--
$| x \rangle\!\langle y | | z \rangle\!\langle w | = \langle y, z \rangle \cdot  | x \rangle\!\langle w |$ -/
@[simp]
theorem rankOne.apply_rankOne (x y z w : E) :
    (rankOne x y : E â†’L[ğ•œ] E) âˆ˜L (rankOne z w : _) = âŸªy,zâŸ«_ğ•œ â€¢ (rankOne x w : _) :=
  by
  ext r
  simp_rw [ContinuousLinearMap.smul_apply, comp_apply, rankOne_apply, inner_smul_right, mul_comm, smul_smul]

/-- $u \circ | x \rangle\!\langle y | = | u(x) \rangle\!\langle y |$ -/
theorem ContinuousLinearMap.comp_rankOne (x y : E) (u : E â†’L[ğ•œ] E) : u âˆ˜L rankOne x y = rankOne (u x) y :=
  by
  ext r
  simp_rw [comp_apply, rankOne_apply, map_smul]

/-- $| x \rangle\!\langle y | \circ u  = | x \rangle\!\langle u^*(y) |$ -/
theorem ContinuousLinearMap.rankOne_comp [CompleteSpace E] (x y : E) (u : E â†’L[ğ•œ] E) :
    rankOne x y âˆ˜L (u : E â†’L[ğ•œ] E) = rankOne x (adjoint u y) :=
  by
  ext r
  simp_rw [comp_apply, rankOne_apply, adjoint_inner_left]

/-- rank one operators given by norm one vectors are automatically idempotent -/
@[simp]
theorem rankOne.isIdempotentElem (x : E) (h : â€–xâ€– = 1) : IsIdempotentElem (rankOne x x : E â†’L[ğ•œ] E) := by
  simp_rw [IsIdempotentElem, ContinuousLinearMap.ext_iff, mul_def, rankOne.apply_rankOne,
    inner_self_eq_norm_sq_to_K, h, IsROrC.ofReal_one, one_pow, one_smul,
    forall_const]

theorem rankOne.isSymmetric (x : E) : LinearMap.IsSymmetric ((rankOne x x : E â†’L[ğ•œ] E) : E â†’â‚—[ğ•œ] E) := by
  simp_rw [LinearMap.IsSymmetric, ContinuousLinearMap.coe_coe, rankOne_apply, inner_smul_left,
    inner_smul_right, inner_conj_symm, mul_comm, forallâ‚‚_true_iff]

/-- rank one operators are automatically self-adjoint -/
@[simp]
theorem rankOne.isSelfAdjoint [CompleteSpace E] (x : E) : IsSelfAdjoint (rankOne x x : E â†’L[ğ•œ] E) :=
  isSelfAdjoint_iff_isSymmetric.mpr (rankOne.isSymmetric x)

/-- $| x \rangle\!\langle y |^* = | y \rangle\!\langle x |$ -/
theorem rankOne.adjoint [CompleteSpace E] (x y : E) : adjoint (rankOne x y) = (rankOne y x : E â†’L[ğ•œ] E) :=
  by
  ext a
  apply @ext_inner_right ğ•œ
  intro b
  simp_rw [adjoint_inner_left, rankOne_apply, inner_smul_left, inner_smul_right, inner_conj_symm,
    mul_comm]

theorem rankOne_inner_left (x y z w : E) : âŸª(rankOne x y : E â†’L[ğ•œ] E) z,wâŸ«_ğ•œ = âŸªz,yâŸ«_ğ•œ * âŸªx,wâŸ«_ğ•œ := by
  rw [rankOne_apply, inner_smul_left, inner_conj_symm]

theorem rankOne_inner_right (x y z w : E) : âŸªx,(rankOne y z : E â†’L[ğ•œ] E) wâŸ«_ğ•œ = âŸªz,wâŸ«_ğ•œ * âŸªx,yâŸ«_ğ•œ := by
  rw [rankOne_apply, inner_smul_right]

theorem ContinuousLinearMap.commutes_with_all_iff [CompleteSpace E] (T : E â†’L[ğ•œ] E) :
    (âˆ€ S : E â†’L[ğ•œ] E, Commute S T) â†” âˆƒ Î± : ğ•œ, T = Î± â€¢ 1 :=
  by
  constructor
  Â· intro h
    have h' : âˆ€ x y : E, rankOne x y âˆ˜L T = T âˆ˜L rankOne x y := fun x y => h _
    simp_rw [ContinuousLinearMap.rankOne_comp, ContinuousLinearMap.comp_rankOne] at h'
    have h'' : âˆ€ x y : E, (rankOne (adjoint T y) x : E â†’L[ğ•œ] E) = rankOne y (T x) :=
      by
      intro x y
      nth_rw 1 [â† rankOne.adjoint]
      rw [h', rankOne.adjoint]
    simp_rw [ext_iff, rankOne_apply] at h' h''
    by_cases H : âˆ€ x : E, x = 0
    Â· use 0
      simp_rw [ext_iff]
      intro x
      rw [H x, zero_smul, map_zero, zero_apply]
    push_neg at H
    obtain âŸ¨x, hxâŸ© := H
    useâŸªx,xâŸ«_ğ•œâ»Â¹ * âŸªadjoint T x,xâŸ«_ğ•œ
    simp_rw [ext_iff, ContinuousLinearMap.smul_apply, one_apply, mul_smul, h', smul_smul]
    rw [inv_mul_cancel]
    simp_rw [one_smul, forall_true_iff]
    Â· rw [inner_self_ne_zero]
      exact hx
  Â· rintro âŸ¨Î±, hÎ±âŸ© S
    simp_rw [Commute, SemiconjBy, mul_def, hÎ±, comp_smul, smul_comp, one_def, comp_id, id_comp]

theorem ContinuousLinearMap.centralizer [CompleteSpace E] :
    (@Set.univ (E â†’L[ğ•œ] E)).centralizer = { x : E â†’L[ğ•œ] E | âˆƒ Î± : ğ•œ, x = Î± â€¢ 1 } :=
  by
  simp_rw [Set.centralizer, Set.mem_univ, true_imp_iff, â† ContinuousLinearMap.commutes_with_all_iff]
  rfl

theorem ContinuousLinearMap.scalar_centralizer :
    {x : E â†’L[ğ•œ] E | âˆƒ Î± : ğ•œ, x = Î± â€¢ 1}.centralizer = @Set.univ (E â†’L[ğ•œ] E) :=
  by
  simp_rw [Set.centralizer, Set.ext_iff, Set.mem_setOf, Set.mem_univ, iff_true_iff]
  rintro x y âŸ¨Î±, rflâŸ©
  simp only [Algebra.smul_mul_assoc, one_mul, Algebra.mul_smul_comm, mul_one]

theorem ContinuousLinearMap.centralizer_centralizer [CompleteSpace E] :
    (@Set.univ (E â†’L[ğ•œ] E)).centralizer.centralizer = Set.univ := by
  rw [ContinuousLinearMap.centralizer, ContinuousLinearMap.scalar_centralizer]

theorem rankOne.zero_left {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
    (x : E) : (rankOne 0 x : E â†’L[ğ•œ] E) = 0 := by
  ext1
  simp_rw [rankOne_apply, smul_zero, ContinuousLinearMap.zero_apply]

theorem rankOne.zero_right {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
    (x : E) : (rankOne x 0 : E â†’L[ğ•œ] E) = 0 := by
  ext1
  simp_rw [rankOne_apply, inner_zero_left, zero_smul, ContinuousLinearMap.zero_apply]

theorem rankOne.ext_iff {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
    (x y : E) (h : (rankOne x x : E â†’L[ğ•œ] E) = rankOne y y) : âˆƒ Î± : ğ•œË£, x = (Î± : ğ•œ) â€¢ y :=
  by
  have : x = 0 â†” y = 0 :=
    by
    constructor <;> intro hh <;>
      simp only [hh, ContinuousLinearMap.ext_iff, rankOne.zero_left, ContinuousLinearMap.zero_apply,
        @eq_comm _ (0 : E â†’L[ğ•œ] E), rankOne_apply, smul_eq_zero] at h
    on_goal 1 => specialize h y
    on_goal 2 => specialize h x
    all_goals
      simp_rw [inner_self_eq_zero, or_self_iff] at h
      exact h
  simp_rw [ContinuousLinearMap.ext_iff, rankOne_apply] at h
  by_cases Hx : x = 0
  Â· use 1
    simp_rw [Hx, Units.val_one, one_smul, eq_comm, â† this, Hx]
  Â· have ugh : inner y x â‰  (0 : ğ•œ) := by
      intro hy
      specialize h x
      rw [hy, zero_smul, smul_eq_zero, inner_self_eq_zero, or_self_iff] at h
      contradiction
    use Units.mk0 (inner y x / inner x x)
        (div_ne_zero ugh ((@inner_self_ne_zero ğ•œ _ _ _ _ _).mpr Hx))
    simp_rw [div_eq_inv_mul, Units.val_mk0, mul_smul, â† h, smul_smul,
      inv_mul_cancel ((@inner_self_ne_zero ğ•œ _ _ _ _ _).mpr Hx), one_smul]

theorem ContinuousLinearMap.ext_inner_map {F : Type _} [NormedAddCommGroup F]
    [InnerProductSpace ğ•œ F] (T S : E â†’L[ğ•œ] F) : T = S â†” âˆ€ x y, âŸªT x,yâŸ«_ğ•œ = âŸªS x,yâŸ«_ğ•œ :=
  by
  simp only [ContinuousLinearMap.ext_iff]
  constructor
  Â· intro h x y
    rw [h]
  Â· intro h x
    apply @ext_inner_right ğ•œ
    exact h x

open scoped BigOperators

local notation "|" x "âŸ©âŸ¨" y "|" => rankOne x y

/- ./././Mathport/Syntax/Translate/Expr.lean:107:6: warning: expanding binder group (x_1 x_2) -/
theorem ContinuousLinearMap.exists_sum_rankOne [FiniteDimensional ğ•œ E] (T : E â†’L[ğ•œ] E) :
    âˆƒ x y : Fin (FiniteDimensional.finrank ğ•œ E) Ã— Fin (FiniteDimensional.finrank ğ•œ E) â†’ E,
      T = âˆ‘ i, |x iâŸ©âŸ¨y i| :=
  by
  letI := FiniteDimensional.complete ğ•œ E
  let e := stdOrthonormalBasis ğ•œ E
  let a : Fin (FiniteDimensional.finrank ğ•œ E) Ã— Fin (FiniteDimensional.finrank ğ•œ E) â†’ E := fun ij =>
    e.repr (T (e ij.1)) ij.2 â€¢ e ij.2
  let b : Fin (FiniteDimensional.finrank ğ•œ E) Ã— Fin (FiniteDimensional.finrank ğ•œ E) â†’ E := fun ij =>
    e ij.1
  refine' âŸ¨a, b, _âŸ©
  simp only [a, b]
  simp only [ContinuousLinearMap.ext_inner_map, sum_apply, sum_inner, rankOne_inner_left,
    inner_smul_left, OrthonormalBasis.repr_apply_apply, inner_conj_symm]
  intro u v
  symm
  calc
    âˆ‘ x : Fin (FiniteDimensional.finrank ğ•œ E) Ã— Fin (FiniteDimensional.finrank ğ•œ E),
          âŸªu,e x.fstâŸ«_ğ•œ * (âŸªT (e x.fst),e x.sndâŸ«_ğ•œ * âŸªe x.snd,vâŸ«_ğ•œ) =
        âˆ‘ x_1 : Fin (FiniteDimensional.finrank ğ•œ E),
        âˆ‘ x_2 : Fin (FiniteDimensional.finrank ğ•œ E),
          âŸªu,e x_1âŸ«_ğ•œ * (âŸªT (e x_1),e x_2âŸ«_ğ•œ * âŸªe x_2,vâŸ«_ğ•œ) :=
      by simp_rw [â† Finset.sum_product', Finset.univ_product_univ]
    _ = âˆ‘ x_1, âŸªu,e x_1âŸ«_ğ•œ * âŸªT (e x_1),vâŸ«_ğ•œ := by
      simp_rw [â† Finset.mul_sum, OrthonormalBasis.sum_inner_mul_inner]
    _ = âŸªT u,vâŸ«_ğ•œ := by simp_rw [â† adjoint_inner_right T, OrthonormalBasis.sum_inner_mul_inner]

theorem LinearMap.exists_sum_rankOne [FiniteDimensional ğ•œ E] (T : E â†’â‚—[ğ•œ] E) :
    âˆƒ x y : Fin (FiniteDimensional.finrank ğ•œ E) Ã— Fin (FiniteDimensional.finrank ğ•œ E) â†’ E,
      T = âˆ‘ i, â†‘(|x iâŸ©âŸ¨y i| : E â†’L[ğ•œ] E) :=
  by
  obtain âŸ¨a, b, hâŸ© := ContinuousLinearMap.exists_sum_rankOne (toContinuousLinearMap T)
  refine' âŸ¨a, b, _âŸ©
  simp_rw [â† coe_sum, â† h]
  rfl

theorem rankOne.sum_orthonormalBasis_eq_id {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E]
    [InnerProductSpace ğ•œ E] {Î¹ : Type _} [Fintype Î¹] (b : OrthonormalBasis Î¹ ğ•œ E) :
    âˆ‘ i, (rankOne (b i) (b i) : E â†’L[ğ•œ] E) = 1 :=
  by
  rw [ContinuousLinearMap.ext_iff]
  intros
  apply @ext_inner_left ğ•œ _
  intro v
  simp_rw [ContinuousLinearMap.sum_apply, rankOne_apply, â† OrthonormalBasis.repr_apply_apply,
    OrthonormalBasis.sum_repr, ContinuousLinearMap.one_apply]

end rankOne

section rankOneLm

variable {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x "," y "âŸ«_ğ•œ" => @inner ğ•œ _ _ x y

/-- same as `rank_one`, but as a linear map -/
def rankOneLm (x y : E) : E â†’â‚—[ğ•œ] E :=
  (rankOne x y).toLinearMap

@[simp]
theorem rankOneLm_apply (x y z : E) : (rankOneLm x y : E â†’â‚—[ğ•œ] E) z = âŸªy,zâŸ«_ğ•œ â€¢ x :=
  rfl

theorem rankOneLm_comp_rankOneLm (x y z w : E) :
    (rankOneLm x y : E â†’â‚—[ğ•œ] E) âˆ˜â‚— rankOneLm z w = âŸªy,zâŸ«_ğ•œ â€¢ (rankOneLm x w : _) := by
  simp_rw [LinearMap.ext_iff, LinearMap.comp_apply, rankOneLm_apply, LinearMap.smul_apply,
    inner_smul_right, mul_smul, rankOneLm_apply, smul_smul, mul_comm,
    forall_true_iff]

theorem rankOneLm_apply_rank_one (x y z w v : E) :
    (rankOneLm x y : E â†’â‚—[ğ•œ] E) ((rankOneLm z w : E â†’â‚—[ğ•œ] E) v) = (âŸªy,zâŸ«_ğ•œ * âŸªw,vâŸ«_ğ•œ) â€¢ x := by
  rw [â† LinearMap.comp_apply, rankOneLm_comp_rankOneLm, LinearMap.smul_apply, rankOneLm_apply,
    smul_smul]

theorem rankOneLm_adjoint [FiniteDimensional ğ•œ E] (x y : E) :
    LinearMap.adjoint (rankOneLm x y : E â†’â‚—[ğ•œ] E) = rankOneLm y x :=
  by
  simp_rw [rankOneLm, LinearMap.adjoint_eq_toCLM_adjoint,
    ContinuousLinearMap.coe_inj, â† @rankOne.adjoint ğ•œ _ _ _ _ (FiniteDimensional.complete ğ•œ E) x y]
  rfl

open scoped BigOperators

theorem sum_rankOne {n : Type _} [Fintype n] (x : n â†’ E) (y : E) :
    (rankOne (âˆ‘ i, x i) y : E â†’L[ğ•œ] E) = âˆ‘ i, rankOne (x i) y :=
  by
  ext1 z
  simp_rw [ContinuousLinearMap.sum_apply, rankOne_apply, Finset.smul_sum]

theorem rankOne_sum {n : Type _} [Fintype n] (x : E) (y : n â†’ E) :
    (rankOne x (âˆ‘ i, y i) : E â†’L[ğ•œ] E) = âˆ‘ i, rankOne x (y i) :=
  by
  ext1 z
  simp_rw [ContinuousLinearMap.sum_apply, rankOne_apply, sum_inner, Finset.sum_smul]

theorem sum_rankOneLm {n : Type _} [Fintype n] (x : n â†’ E) (y : E) :
    (rankOneLm (âˆ‘ i : n, x i) y : E â†’â‚—[ğ•œ] E) = âˆ‘ i : n, rankOneLm (x i) y :=
  by
  rw [rankOneLm, sum_rankOne, ContinuousLinearMap.coe_sum]
  rfl

theorem rankOneLm_sum {n : Type _} [Fintype n] (x : E) (y : n â†’ E) :
    (rankOneLm x (âˆ‘ i : n, y i) : E â†’â‚—[ğ•œ] E) = âˆ‘ i : n, rankOneLm x (y i) :=
  by
  rw [rankOneLm, rankOne_sum, ContinuousLinearMap.coe_sum]
  rfl

end rankOneLm

theorem LinearMap.ext_of_rankOne {ğ•œ H H' : Type _} [IsROrC ğ•œ] [AddCommMonoid H'] [Module ğ•œ H']
    [NormedAddCommGroup H] [InnerProductSpace ğ•œ H] [FiniteDimensional ğ•œ H]
    {x y : (H â†’L[ğ•œ] H) â†’â‚—[ğ•œ] H'} (h : âˆ€ a b : H, x (rankOne a b) = y (rankOne a b)) : x = y :=
  by
  ext a
  obtain âŸ¨Î±, Î², rflâŸ© := ContinuousLinearMap.exists_sum_rankOne a
  simp_rw [map_sum, h]

theorem LinearMap.ext_of_rank_one' {ğ•œ H H' : Type _} [IsROrC ğ•œ] [AddCommMonoid H'] [Module ğ•œ H']
    [NormedAddCommGroup H] [InnerProductSpace ğ•œ H] [FiniteDimensional ğ•œ H]
    {x y : (H â†’â‚—[ğ•œ] H) â†’â‚—[ğ•œ] H'}
    (h : âˆ€ a b : H, x â†‘(@rankOne ğ•œ _ _ _ _ a b) = y â†‘(@rankOne ğ•œ _ _ _ _ a b)) : x = y :=
  by
  ext a
  obtain âŸ¨Î±, Î², rflâŸ© := LinearMap.exists_sum_rankOne a
  simp_rw [map_sum, h]

open scoped BigOperators

theorem rankOne.sum_orthonormalBasis_eq_id_lm {ğ•œ : Type _} {E : Type _} [IsROrC ğ•œ]
    [NormedAddCommGroup E] [InnerProductSpace ğ•œ E] {Î¹ : Type _} [Fintype Î¹]
    (b : OrthonormalBasis Î¹ ğ•œ E) : âˆ‘ i, (@rankOne ğ•œ E _ _ _ (b i) (b i) : E â†’â‚—[ğ•œ] E) = 1 :=
  by
  simp only [â† ContinuousLinearMap.coe_sum, rankOne.sum_orthonormalBasis_eq_id b]
  rfl

theorem ContinuousLinearMap.coe_eq_zero {ğ•œ Eâ‚ Eâ‚‚ : Type _} [IsROrC ğ•œ] [NormedAddCommGroup Eâ‚]
    [NormedAddCommGroup Eâ‚‚] [InnerProductSpace ğ•œ Eâ‚] [InnerProductSpace ğ•œ Eâ‚‚] (f : Eâ‚ â†’L[ğ•œ] Eâ‚‚) :
    (f : Eâ‚ â†’â‚—[ğ•œ] Eâ‚‚) = 0 â†” f = 0 := by norm_cast

theorem rankOne.eq_zero_iff {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
    (x y : E) : (rankOne x y : E â†’L[ğ•œ] E) = 0 â†” x = 0 âˆ¨ y = 0 := by
  simp_rw [ContinuousLinearMap.ext_iff, rankOne_apply, ContinuousLinearMap.zero_apply, smul_eq_zero,
    forall_or_right, forall_inner_eq_zero_iff, or_comm]

theorem rankOne.left_add {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
    (x y z : E) : (rankOne (x + y) z : E â†’L[ğ•œ] E) = rankOne x z + rankOne y z :=
  by
  ext
  simp only [rankOne_apply, ContinuousLinearMap.add_apply, smul_add]

theorem rankOne.right_add {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
    (x y z : E) : (rankOne x (y + z) : E â†’L[ğ•œ] E) = rankOne x y + rankOne x z :=
  by
  ext
  simp only [rankOne_apply, ContinuousLinearMap.add_apply, inner_add_left, add_smul]

theorem rankOne.left_sub {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
    (x y z : E) : (rankOne (x - y) z : E â†’L[ğ•œ] E) = rankOne x z - rankOne y z :=
  by
  ext
  simp only [rankOne_apply, ContinuousLinearMap.sub_apply, smul_sub]

theorem rankOne.right_sub {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
    (x y z : E) : (rankOne x (y - z) : E â†’L[ğ•œ] E) = rankOne x y - rankOne x z :=
  by
  ext
  simp only [rankOne_apply, ContinuousLinearMap.sub_apply, inner_sub_left, sub_smul]

theorem LinearMap.rankOne_comp {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E]
    [InnerProductSpace ğ•œ E] [FiniteDimensional ğ•œ E] (x y : E) (u : E â†’â‚—[ğ•œ] E) :
    ((rankOne x y : E â†’L[ğ•œ] E) : E â†’â‚—[ğ•œ] E) âˆ˜â‚— u = (rankOne x (adjoint u y) : E â†’L[ğ•œ] E) :=
  by
  ext
  simp_rw [LinearMap.comp_apply, ContinuousLinearMap.coe_coe, rankOne_apply,
    LinearMap.adjoint_inner_left]

theorem LinearMap.rankOne_comp' {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E]
    [InnerProductSpace ğ•œ E] [FiniteDimensional ğ•œ E] (x y : E) (u : E â†’â‚—[ğ•œ] E) :
    ((rankOne x y : E â†’L[ğ•œ] E) : E â†’â‚—[ğ•œ] E) âˆ˜â‚— adjoint u = (rankOne x (u y) : E â†’L[ğ•œ] E) := by
  rw [LinearMap.rankOne_comp, LinearMap.adjoint_adjoint]

theorem OrthonormalBasis.orthogonalProjection'_eq_sum_rankOne {Î¹ ğ•œ : Type _} [IsROrC ğ•œ] {E : Type _}
    [NormedAddCommGroup E] [InnerProductSpace ğ•œ E] [Fintype Î¹] {U : Submodule ğ•œ E}
    [CompleteSpace â†¥U] (b : OrthonormalBasis Î¹ ğ•œ â†¥U) :
    orthogonalProjection' U = âˆ‘ i : Î¹, rankOne (b i : E) (b i : E) :=
  by
  ext
  simp_rw [orthogonalProjection'_apply, b.orthogonalProjection_eq_sum,
    ContinuousLinearMap.sum_apply, rankOne_apply, Submodule.coe_sum, Submodule.coe_smul_of_tower]

theorem LinearMap.comp_rankOne {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E]
    [InnerProductSpace ğ•œ E] (x y : E) (u : E â†’â‚—[ğ•œ] E) :
    u âˆ˜â‚— ((rankOne x y : E â†’L[ğ•œ] E) : E â†’â‚—[ğ•œ] E) = (rankOne (u x) y : E â†’L[ğ•œ] E) :=
  by
  ext
  simp_rw [LinearMap.comp_apply, ContinuousLinearMap.coe_coe, rankOne_apply, SMulHomClass.map_smul]
