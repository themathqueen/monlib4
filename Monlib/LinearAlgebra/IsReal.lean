/-
Copyright (c) 2023 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
import Mathlib.Algebra.Star.StarAlgHom
import Mathlib.Algebra.Star.BigOperators
-- import Monlib.LinearAlgebra.InnerAut
import Mathlib.Algebra.Algebra.Spectrum
import Monlib.LinearAlgebra.End
import Monlib.Preq.StarAlgEquiv
import Mathlib.Analysis.RCLike.Basic
import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Algebra.Algebra.Bilinear
import Mathlib.Algebra.Algebra.Basic

#align_import linear_algebra.is_real

/-!
 # real linear maps (a.k.a. star-preserving linear maps)

 This file defines the function `linear_map.real` which maps a linear map `Ï†.real (x) = star (Ï† (star x))`; so that `Ï†` is real (star-preserving) if and only if `Ï† = Ï†.real`.
-/


def LinearMap.IsReal {E F K : Type _} [Semiring K] [AddCommMonoid E] [AddCommMonoid F] [Module K E]
    [Module K F] [Star E] [Star F] (Ï† : E â†’â‚—[K] F) : Prop :=
  âˆ€ x, Ï† (star x) = star (Ï† x)

section Sec

variable {E F K : Type _} [AddCommMonoid E] [StarAddMonoid E] [AddCommMonoid F] [StarAddMonoid F]
  [Semiring K] [Module K E] [Module K F] [InvolutiveStar K] [StarModule K E] [StarModule K F]

@[simps]
def LinearMap.real (Ï† : E â†’â‚—[K] F) :
    E â†’â‚—[K] F where
  toFun x := star (Ï† (star x))
  map_add' _ _ := by simp only [star_add, map_add]
  map_smul' _ _ := by simp only [star_smul, _root_.map_smul, star_star, RingHom.id_apply]

theorem LinearMap.isReal_iff (Ï† : E â†’â‚—[K] F) : Ï†.IsReal â†” Ï†.real = Ï† := by
  simp_rw [LinearMap.IsReal, LinearMap.ext_iff, LinearMap.real_apply,
    @eq_star_iff_eq_star _ _ (Ï† (star _)), eq_comm]

theorem LinearMap.real_add (f g : E â†’â‚—[K] F) : (f + g).real = f.real + g.real :=
  by
  ext
  simp only [LinearMap.real_apply, LinearMap.add_apply, star_add]

open scoped BigOperators

theorem LinearMap.real_sum {n : Type _} {s : Finset n} (f : n â†’ E â†’â‚—[K] F) :
    (âˆ‘ i : n in s, f i).real = âˆ‘ i : n in s, (f i).real :=
  by
  ext
  simp only [LinearMap.real_apply, LinearMap.sum_apply, star_sum]

theorem LinearMap.real_real (f : E â†’â‚—[K] F) : f.real.real = f :=
  by
  ext
  simp only [LinearMap.real_apply, star_star]

theorem LinearMap.real_comp {G : Type _} [AddCommMonoid G] [StarAddMonoid G] [Module K G]
    [StarModule K G] (f : E â†’â‚—[K] F) (g : G â†’â‚—[K] E) : (f âˆ˜â‚— g).real = f.real âˆ˜â‚— g.real :=
  by
  ext
  simp only [LinearMap.real_apply, LinearMap.comp_apply, star_star]

theorem LinearMap.real_starAlgEquiv_conj {E K : Type _} [CommSemiring K] [Semiring E] [Algebra K E]
    [InvolutiveStar K] [StarAddMonoid E] [StarModule K E] (f : E â†’â‚—[K] E) (Ï† : E â‰ƒâ‹†â‚[K] E) :
    (Ï†.toAlgEquiv.toLinearEquiv.toLinearMap âˆ˜â‚—
          f âˆ˜â‚— Ï†.symm.toAlgEquiv.toLinearEquiv.toLinearMap).real =
      Ï†.toAlgEquiv.toLinearEquiv.toLinearMap âˆ˜â‚—
        f.real âˆ˜â‚— Ï†.symm.toAlgEquiv.toLinearEquiv.toLinearMap :=
  by
  ext
  simp only [LinearMap.real_apply, LinearMap.comp_apply, LinearEquiv.coe_toLinearMap,
    AlgEquiv.toLinearEquiv_apply, StarAlgEquiv.coe_toAlgEquiv, map_star]

theorem LinearMap.real_starAlgEquiv_conj_iff {E K : Type _} [CommSemiring K] [Semiring E]
    [Algebra K E] [InvolutiveStar K] [StarAddMonoid E] [StarModule K E] (f : E â†’â‚—[K] E)
    (Ï† : E â‰ƒâ‹†â‚[K] E) :
    (Ï†.toAlgEquiv.toLinearEquiv.toLinearMap âˆ˜â‚—
          f âˆ˜â‚— Ï†.symm.toAlgEquiv.toLinearEquiv.toLinearMap).IsReal â†”
      f.IsReal :=
  by
  simp_rw [LinearMap.isReal_iff, LinearMap.real_starAlgEquiv_conj, LinearMap.ext_iff,
    LinearMap.comp_apply, LinearEquiv.coe_toLinearMap, AlgEquiv.toLinearEquiv_apply,
    StarAlgEquiv.coe_toAlgEquiv, â† StarAlgEquiv.symm_apply_eq, StarAlgEquiv.symm_apply_apply]
  refine' âŸ¨fun h x => _, fun h x => h _âŸ©
  specialize h (Ï† x)
  simp_rw [StarAlgEquiv.symm_apply_apply] at h
  exact h

def LinearMap.realRingEquiv {R E : Type _} [Semiring R] [AddCommMonoid E] [StarAddMonoid E]
    [Module R E] [InvolutiveStar R] [StarModule R E] : (E â†’â‚—[R] E) â‰ƒ+* (E â†’â‚—[R] E)
    where
  toFun f := f.real
  invFun f := f.real
  map_add' _ _ := LinearMap.real_add _ _
  map_mul' _ _ := LinearMap.real_comp _ _
  left_inv _ := LinearMap.real_real _
  right_inv _ := LinearMap.real_real _

theorem LinearMap.mulRight_real {E K : Type _} [CommSemiring K] [NonUnitalSemiring E]
    [InvolutiveStar K] [StarRing E] [Module K E] [StarModule K E] [SMulCommClass K E E]
    [IsScalarTower K E E] (x : E) : (LinearMap.mulRight K x).real = LinearMap.mulLeft K (star x) :=
  by
  ext u
  simp_rw [LinearMap.real_apply, LinearMap.mulRight_apply, LinearMap.mulLeft_apply, star_mul,
    star_star]

theorem LinearMap.mulLeft_real {E K : Type _} [CommSemiring K] [NonUnitalSemiring E]
    [InvolutiveStar K] [StarRing E] [Module K E] [StarModule K E] [SMulCommClass K E E]
    [IsScalarTower K E E] (x : E) : (LinearMap.mulLeft K x).real = LinearMap.mulRight K (star x) :=
  by
  ext u
  simp_rw [LinearMap.real_apply, LinearMap.mulRight_apply, LinearMap.mulLeft_apply, star_mul,
    star_star]

end Sec

variable {ğ•œ E : Type _} [RCLike ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E] [StarAddMonoid E]
  [StarModule ğ•œ E] [FiniteDimensional ğ•œ E]

theorem LinearMap.real.spectrum (Ï† : E â†’â‚—[ğ•œ] E) : spectrum ğ•œ Ï†.real = star (spectrum ğ•œ Ï†) :=
  by
  ext
  simp_rw [Set.mem_star, â† Module.End.hasEigenvalue_iff_mem_spectrum, â†
    Module.End.has_eigenvector_iff_hasEigenvalue, LinearMap.real_apply, star_eq_iff_star_eq, star_smul]
  constructor <;> rintro âŸ¨v, âŸ¨h, hvâŸ©âŸ©
  Â· exact âŸ¨star v, h.symm, star_ne_zero.mpr hvâŸ©
  Â· refine' âŸ¨star v, _, star_ne_zero.mpr hvâŸ©
    rw [star_star]
    exact h.symm

theorem LinearMap.real.eigenspace {E : Type _} [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
    [StarAddMonoid E] [StarModule ğ•œ E] (Ï† : E â†’â‚—[ğ•œ] E) (Î± : ğ•œ) (x : E) :
    x âˆˆ Module.End.eigenspace Ï†.real Î± â†” star x âˆˆ Module.End.eigenspace Ï† (star Î±) := by
  simp_rw [Module.End.mem_eigenspace_iff, LinearMap.real_apply, star_eq_iff_star_eq, star_smul,
    eq_comm]

theorem LinearMap.real_neg {E : Type _} {F : Type _} {K : Type _} [AddCommMonoid E]
    [StarAddMonoid E] [AddCommGroup F] [StarAddMonoid F] [Semiring K] [Module K E] [Module K F]
    [InvolutiveStar K] [StarModule K E] [StarModule K F] (f : E â†’â‚—[K] F) : (-f).real = -f.real :=
  by
  ext
  simp only [LinearMap.neg_apply, LinearMap.real_apply, star_neg]

theorem LinearMap.real_sub {E : Type _} {F : Type _} {K : Type _} [AddCommMonoid E]
    [StarAddMonoid E] [AddCommGroup F] [StarAddMonoid F] [Semiring K] [Module K E] [Module K F]
    [InvolutiveStar K] [StarModule K E] [StarModule K F] (f g : E â†’â‚—[K] F) :
    (f - g).real = f.real - g.real :=
  by
  simp_rw [sub_eq_add_neg, â† LinearMap.real_neg]
  exact LinearMap.real_add _ _

theorem LinearMap.real_smul {E F K : Type _} [CommSemiring K] [AddCommMonoid E] [AddCommMonoid F]
    [StarRing K] [StarAddMonoid E] [StarAddMonoid F] [Module K E] [Module K F] [StarModule K E]
    [StarModule K F] (f : E â†’â‚—[K] F) (Î± : K) : (Î± â€¢ f).real = starRingEnd K Î± â€¢ f.real :=
  by
  ext
  simp_rw [LinearMap.real_apply, LinearMap.smul_apply, star_smul, starRingEnd_apply]
  rfl

theorem LinearMap.real_inj_eq {E F K : Type _} [Semiring K] [AddCommMonoid E] [AddCommMonoid F]
    [InvolutiveStar K] [StarAddMonoid E] [StarAddMonoid F] [Module K E] [Module K F]
    [StarModule K E] [StarModule K F] (f g : E â†’â‚—[K] F) : f = g â†” f.real = g.real :=
  by
  refine' âŸ¨fun h => by rw [h], fun h => _âŸ©
  rw [â† LinearMap.real_real f, h, LinearMap.real_real]

theorem LinearMap.isRealOne {E K : Type _} [Semiring K] [AddCommMonoid E] [Module K E] [Star E] :
    (1 : E â†’â‚—[K] E).IsReal := fun _ => rfl

theorem LinearMap.real_one {E K : Type _} [Semiring K] [InvolutiveStar K] [AddCommMonoid E]
    [StarAddMonoid E] [Module K E] [StarModule K E] : (1 : E â†’â‚—[K] E).real = 1 :=
  (LinearMap.isReal_iff _).mp LinearMap.isRealOne
