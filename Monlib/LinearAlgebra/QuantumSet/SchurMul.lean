/-
Copyright (c) 2024 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
-- import Monlib.LinearAlgebra.MyIps.Nontracial
-- import Monlib.LinearAlgebra.MyIps.MatIps
import Monlib.LinearAlgebra.QuantumSet.Basic
import Monlib.LinearAlgebra.Ips.TensorHilbert
import Monlib.LinearAlgebra.IsReal
-- import Monlib.LinearAlgebra.MyIps.Frob
import Monlib.LinearAlgebra.TensorFinite
import Monlib.LinearAlgebra.Ips.OpUnop
import Monlib.LinearAlgebra.LmulRmul
import Monlib.LinearAlgebra.Nacgor
import Monlib.LinearAlgebra.Coalgebra.FiniteDimensional

#align_import quantum_graph.schur_idempotent

/-!
 # Schur product operator

 In this file we define the Schur product operator and prove some basic properties.
-/

open scoped TensorProduct BigOperators

local notation "l(" x ")" => x â†’â‚—[â„‚] x

local notation "|" x "âŸ©âŸ¨" y "|" => @rankOne â„‚ _ _ _ _ _ _ _ x y

local notation "m" x => LinearMap.mul' â„‚ x

local notation x " âŠ—â‚˜ " y => TensorProduct.map x y

open Coalgebra
/-- Schur product `â¬ â€¢â‚› â¬ : (B â†’ C) â†’ (B â†’ C) â†’ (B â†’ C)` given by
  `x â€¢â‚› y := m âˆ˜ (x âŠ— y) âˆ˜ comul`  -/
@[simps]
noncomputable def schurMul {B C : Type _}
  [NormedAddCommGroupOfRing B] [NormedAddCommGroupOfRing C]
  [hB : QuantumSet B] [hC : QuantumSet C] :
  (B â†’â‚—[â„‚] C) â†’â‚—[â„‚] (B â†’â‚—[â„‚] C) â†’â‚—[â„‚] (B â†’â‚—[â„‚] C)
    where
  toFun x :=
    { toFun := fun y => (m C) âˆ˜â‚— (x âŠ—â‚˜ y) âˆ˜â‚— comul
      map_add' := fun x y => by
        simp only [TensorProduct.map_apply, TensorProduct.map_add_right, LinearMap.add_comp,
          LinearMap.comp_add]
      map_smul' := fun r x => by
        simp only [TensorProduct.map_smul_right, LinearMap.smul_comp, LinearMap.comp_smul,
          RingHom.id_apply] }
  map_add' x y :=
    by
    simp only [TensorProduct.map_add_left, LinearMap.add_comp, LinearMap.comp_add,
      LinearMap.ext_iff, LinearMap.add_apply, LinearMap.coe_mk]
    intro _ _; rfl
  map_smul' r x :=
    by
    simp only [TensorProduct.map_smul_left, LinearMap.smul_comp, LinearMap.comp_smul,
      LinearMap.ext_iff, LinearMap.smul_apply, LinearMap.coe_mk, RingHom.id_apply]
    intro _ _; rfl

@[inherit_doc schurMul]
-- scoped[schurMul] infix:100 " â€¢â‚› " => schurMul
notation3:80 (name := schurMulNotation) x:81 " â€¢â‚› " y:80 => schurMul x y
open scoped schurMul

variable {A B C : Type _}
  [NormedAddCommGroupOfRing A] [NormedAddCommGroupOfRing B] [NormedAddCommGroupOfRing C]
  [hA : QuantumSet A] [hB : QuantumSet B] [hC : QuantumSet C]

theorem schurMul.apply_rankOne
  (a : B) (b : C) (c : B) (d : C) : (â†‘|aâŸ©âŸ¨b|) â€¢â‚› (â†‘|câŸ©âŸ¨d|) = (|a * câŸ©âŸ¨b * d| : C â†’â‚—[â„‚] B) :=
  by
  rw [schurMul, LinearMap.ext_iff]
  intro x
  apply ext_inner_right â„‚
  intro u
  simp only [ContinuousLinearMap.coe_coe, LinearMap.coe_mk, AddHom.coe_mk,
    rankOne_apply, LinearMap.comp_apply]
  obtain âŸ¨Î±, Î², hâŸ© := TensorProduct.eq_span (comul x : _ âŠ—[â„‚] _)
  rw [â† h]
  simp_rw [map_sum, TensorProduct.map_tmul, ContinuousLinearMap.coe_coe, rankOne_apply,
    LinearMap.mul'_apply, smul_mul_smul, â† TensorProduct.inner_tmul, â† Finset.sum_smul, â† inner_sum,
    h, comul_eq_mul_adjoint, LinearMap.adjoint_inner_right, LinearMap.mul'_apply]

theorem schurMul.apply_ket
  (a b : B) :
  (ket â„‚ a) â€¢â‚› (ket â„‚ b) = (ket â„‚ (a * b)).toLinearMap :=
by
  simp only [schurMul_apply_apply, QuantumSet.complex_comul]
  ext
  simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply,
    TensorProduct.lid_symm_apply, TensorProduct.map_tmul, ContinuousLinearMap.coe_coe,
    ket_toFun_toFun, one_smul, LinearMap.mul'_apply]

theorem bra_tmul (a : B) (b : C) :
  TensorProduct.map (bra â„‚ a).toLinearMap (bra â„‚ b).toLinearMap
    = (TensorProduct.lid â„‚ _).symm.toLinearMap âˆ˜â‚— ((bra â„‚ (a âŠ—â‚œ[â„‚] b)).toLinearMap) :=
by
  ext
  simp only [TensorProduct.AlgebraTensorModule.curry_apply, TensorProduct.curry_apply,
    LinearMap.coe_restrictScalars, TensorProduct.map_tmul, ContinuousLinearMap.coe_coe,
    innerSL_apply, LinearMap.coe_comp, LinearEquiv.coe_coe, innerSL_apply_coe, Function.comp_apply,
    TensorProduct.inner_tmul, TensorProduct.lid_symm_apply]
  rw [â† smul_eq_mul, â† TensorProduct.smul_tmul, smul_eq_mul, mul_one]

theorem bra_comp_linearMap {ğ•œ Eâ‚ Eâ‚‚ : Type*} [RCLike ğ•œ]
  [NormedAddCommGroup Eâ‚] [InnerProductSpace ğ•œ Eâ‚] [NormedAddCommGroup Eâ‚‚]
  [InnerProductSpace ğ•œ Eâ‚‚] [FiniteDimensional ğ•œ Eâ‚] [FiniteDimensional ğ•œ Eâ‚‚]
  (x : Eâ‚‚) (f : Eâ‚ â†’â‚—[ğ•œ] Eâ‚‚) :
  ((bra ğ•œ) x).toLinearMap.comp f = ((bra ğ•œ) ((LinearMap.adjoint f) x)).toLinearMap :=
letI := FiniteDimensional.complete ğ•œ Eâ‚
letI := FiniteDimensional.complete ğ•œ Eâ‚‚
calc (bra ğ•œ x).toLinearMap âˆ˜â‚— f
    = ((bra ğ•œ x) âˆ˜L LinearMap.toContinuousLinearMap f).toLinearMap := rfl
  _ = (bra ğ•œ ((ContinuousLinearMap.adjoint (LinearMap.toContinuousLinearMap f)) x)).toLinearMap :=
    by rw [bra_comp_continuousLinearMap]
  _ = (bra ğ•œ ((LinearMap.adjoint f) x)).toLinearMap := rfl
theorem linearMap_comp_ket {ğ•œ Eâ‚ Eâ‚‚ : Type*} [RCLike ğ•œ]
  [NormedAddCommGroup Eâ‚] [InnerProductSpace ğ•œ Eâ‚] [NormedAddCommGroup Eâ‚‚]
  [InnerProductSpace ğ•œ Eâ‚‚] (x : Eâ‚) (f : Eâ‚ â†’â‚—[ğ•œ] Eâ‚‚) :
  f âˆ˜â‚— (ket ğ•œ x).toLinearMap = (ket ğ•œ (f x)).toLinearMap :=
by
  ext
  simp only [LinearMap.coe_comp, ContinuousLinearMap.coe_coe, Function.comp_apply, ket_toFun_toFun,
    one_smul]

theorem mul_comp_lid_symm {R : Type*} [CommSemiring R] :
  LinearMap.mul' R R âˆ˜â‚— (TensorProduct.lid R R).symm.toLinearMap = LinearMap.id :=
by aesop

theorem schurMul.apply_bra
  (a b : B) :
  (bra â„‚ a) â€¢â‚› (bra â„‚ b) = (bra â„‚ (a * b)).toLinearMap :=
by
  rw [schurMul_apply_apply, bra_tmul, LinearMap.comp_assoc, bra_comp_linearMap,
    Coalgebra.comul_eq_mul_adjoint, LinearMap.adjoint_adjoint, LinearMap.mul'_apply,
    â† LinearMap.comp_assoc, mul_comp_lid_symm]
  rfl

theorem schurMul.comp_apply_of
  (Î´ : â„‚)
  (hAÎ´ : Coalgebra.comul âˆ˜â‚— LinearMap.mul' â„‚ A = Î´ â€¢ LinearMap.id)
  (a b : A â†’â‚—[â„‚] B) (c d : C â†’â‚—[â„‚] A) :
  (a â€¢â‚› b) âˆ˜â‚— (c â€¢â‚› d) = Î´ â€¢ ((a âˆ˜â‚— c) â€¢â‚› (b âˆ˜â‚— d)) :=
by
  calc (a â€¢â‚› b) âˆ˜â‚— (c â€¢â‚› d)
      = (m _) âˆ˜â‚— (a âŠ—â‚˜ b) âˆ˜â‚— (comul âˆ˜â‚— (m A)) âˆ˜â‚— (c âŠ—â‚˜ d) âˆ˜â‚— comul :=
        by simp_rw [schurMul_apply_apply, LinearMap.comp_assoc]
    _ = Î´ â€¢ (m _ ) âˆ˜â‚— ((a âŠ—â‚˜ b) âˆ˜â‚— (c âŠ—â‚˜ d)) âˆ˜â‚— comul :=
        by simp_rw [hAÎ´, LinearMap.smul_comp, LinearMap.comp_smul, LinearMap.id_comp,
          LinearMap.comp_assoc]
    _ = Î´ â€¢ (a âˆ˜â‚— c) â€¢â‚› (b âˆ˜â‚— d) :=
        by rw [â† TensorProduct.map_comp]; rfl


theorem schurMul_one_one_right
    (A : C â†’â‚—[â„‚] B) : A â€¢â‚› (|(1 : B)âŸ©âŸ¨(1 : C)| : C â†’â‚—[â„‚] B) = A :=
  by
  obtain âŸ¨Î±, Î², rflâŸ© := LinearMap.exists_sum_rankOne A
  simp_rw [map_sum, LinearMap.sum_apply, schurMul.apply_rankOne, mul_one]

theorem schurMul_one_one_left
    (A : C â†’â‚—[â„‚] B) : (|(1 : B)âŸ©âŸ¨(1 : C)| : C â†’â‚—[â„‚] B) â€¢â‚› A = A :=
  by
  obtain âŸ¨Î±, Î², rflâŸ© := LinearMap.exists_sum_rankOne A
  simp_rw [map_sum, schurMul.apply_rankOne, one_mul]

theorem schurMul_adjoint (x y : B â†’â‚—[â„‚] C) :
    LinearMap.adjoint (x â€¢â‚› y) = (LinearMap.adjoint x) â€¢â‚› (LinearMap.adjoint y) :=
  by
  simp_rw [schurMul, comul_eq_mul_adjoint]
  simp only [LinearMap.coe_mk, AddHom.coe_mk, LinearMap.adjoint_comp, LinearMap.adjoint_adjoint,
    TensorProduct.map_adjoint, LinearMap.comp_assoc]

theorem schurMul_real (x y : A â†’â‚—[â„‚] B) :
    LinearMap.real (x â€¢â‚› y : A â†’â‚—[â„‚] B) = (LinearMap.real y) â€¢â‚› (LinearMap.real x) :=
  by
  obtain âŸ¨Î±, Î², rflâŸ© := x.exists_sum_rankOne
  obtain âŸ¨Î³, Î¶, rflâŸ© := y.exists_sum_rankOne
  simp only [map_sum, LinearMap.real_sum, LinearMap.sum_apply, schurMul.apply_rankOne]
  simp_rw [rankOne_real, schurMul.apply_rankOne, â† map_mul, â† StarMul.star_mul]
  rw [Finset.sum_comm]

theorem schurMul_one_right_rankOne (a b : B) :
    (â†‘|aâŸ©âŸ¨b|) â€¢â‚› 1 = lmul a * (LinearMap.adjoint (lmul b : l(B))) :=
  by
  simp_rw [LinearMap.ext_iff_inner_map]
  intro u
  let f := stdOrthonormalBasis â„‚ B
  rw [â† rankOne.sum_orthonormalBasis_eq_id_lm f]
  simp_rw [map_sum, LinearMap.sum_apply, schurMul.apply_rankOne, ContinuousLinearMap.coe_coe,
    rankOne_apply, LinearMap.mul_apply, lmul_apply, sum_inner, inner_smul_left,
    hB.inner_star_left,
    inner_conj_symm, OrthonormalBasis.sum_inner_mul_inner, lmul_adjoint, lmul_apply]

theorem schurMul_one_left_rankOne (a b : B) :
    (1 : l(B)) â€¢â‚› (|aâŸ©âŸ¨b|) = (rmul a : l(B)) * (LinearMap.adjoint (rmul b : l(B))) :=
  by
  simp_rw [â† ext_inner_map]
  intro u
  let f := stdOrthonormalBasis â„‚ B
  rw [â† rankOne.sum_orthonormalBasis_eq_id_lm f, map_sum, LinearMap.sum_apply]
  simp_rw [schurMul.apply_rankOne, LinearMap.sum_apply, ContinuousLinearMap.coe_coe,
    rankOne_apply, LinearMap.mul_apply, rmul_apply, sum_inner, inner_smul_left,
    QuantumSet.inner_conj_left, inner_conj_symm,
    OrthonormalBasis.sum_inner_mul_inner, â† QuantumSet.inner_conj_left,
    rmul_adjoint, rmul_apply]

theorem Psi.schurMul (râ‚ râ‚‚ : â„)
    (f g : A â†’â‚—[â„‚] B) :
    hA.Psi râ‚ râ‚‚ (f â€¢â‚› g) = hA.Psi râ‚ râ‚‚ f * hA.Psi râ‚ râ‚‚ g :=
  by
  suffices
    âˆ€ (a c : B) (b d : A),
      hA.Psi râ‚ râ‚‚ ((â†‘|aâŸ©âŸ¨b|) â€¢â‚› |câŸ©âŸ¨d|) = hA.Psi râ‚ râ‚‚ (|aâŸ©âŸ¨b|).toLinearMap * hA.Psi râ‚ râ‚‚ (|câŸ©âŸ¨d|)
    by
    obtain âŸ¨Î±, Î², rflâŸ© := f.exists_sum_rankOne
    obtain âŸ¨Î³, Î´, rflâŸ© := g.exists_sum_rankOne
    simp_rw [map_sum, LinearMap.sum_apply, Finset.mul_sum, Finset.sum_mul, map_sum, this]
  intro a b c d
  simp_rw [hA.Psi_apply, hA.Psi_toFun_apply, schurMul.apply_rankOne,
    hA.Psi_toFun_apply, Algebra.TensorProduct.tmul_mul_tmul,
    â† MulOpposite.op_mul, â† StarMul.star_mul, â† map_mul]

theorem schurMul_assoc (f g h : A â†’â‚—[â„‚] B) :
  (f â€¢â‚› g) â€¢â‚› h = f â€¢â‚› (g â€¢â‚› h) :=
by
  apply_fun hA.Psi 0 0 using LinearEquiv.injective _
  simp_rw [Psi.schurMul, mul_assoc]

theorem algHom_comp_mul {R A B : Type*} [CommSemiring R] [Semiring A]
  [Semiring B] [Algebra R A] [Algebra R B] (f : A â†’â‚[R] B) :
    f.toLinearMap âˆ˜â‚— LinearMap.mul' R A = (LinearMap.mul' R B) âˆ˜â‚— (f.toLinearMap âŠ—â‚˜ f.toLinearMap) :=
by
  rw [TensorProduct.ext_iff]
  intro a b
  simp only [LinearMap.comp_apply, LinearMap.mul'_apply, AlgHom.toLinearMap_apply, map_mul,
    TensorProduct.map_tmul]

theorem comul_comp_algHom_adjoint (f : A â†’â‚[â„‚] B) :
    Coalgebra.comul âˆ˜â‚— (LinearMap.adjoint f.toLinearMap)
      = ((LinearMap.adjoint f.toLinearMap) âŠ—â‚˜ (LinearMap.adjoint f.toLinearMap)) âˆ˜â‚— Coalgebra.comul :=
by
  simp_rw [comul_eq_mul_adjoint, â† TensorProduct.map_adjoint, â† LinearMap.adjoint_comp,
    algHom_comp_mul]

theorem schurMul_algHom_comp_coalgHom {D : Type*} [NormedAddCommGroupOfRing D]
  [hD : QuantumSet D] (g : C â†’â‚[â„‚] D) (f : A â†’â‚—c[â„‚] B) (x y : B â†’â‚—[â„‚] C) :
  (g.toLinearMap âˆ˜â‚— x âˆ˜â‚— f.toLinearMap) â€¢â‚› (g.toLinearMap âˆ˜â‚— y âˆ˜â‚— f.toLinearMap)
    = g.toLinearMap âˆ˜â‚— (x â€¢â‚› y) âˆ˜â‚— f.toLinearMap :=
by
  simp_rw [schurMul_apply_apply, â† LinearMap.comp_assoc, algHom_comp_mul, LinearMap.comp_assoc,
    â† f.map_comp_comul]
  congr 1
  simp_rw [â† LinearMap.comp_assoc]
  congr 1
  simp_rw [TensorProduct.map_comp]

theorem schurMul_algHom_comp_algHom_adjoint {D : Type*} [NormedAddCommGroupOfRing D]
  [hD : QuantumSet D] (g : C â†’â‚[â„‚] D) (f : B â†’â‚[â„‚] A) (x y : B â†’â‚—[â„‚] C) :
  (g.toLinearMap âˆ˜â‚— x âˆ˜â‚— (LinearMap.adjoint f.toLinearMap))
    â€¢â‚› (g.toLinearMap âˆ˜â‚— y âˆ˜â‚— LinearMap.adjoint f.toLinearMap)
    = g.toLinearMap âˆ˜â‚— (x â€¢â‚› y) âˆ˜â‚— LinearMap.adjoint f.toLinearMap :=
by
  simp_rw [schurMul_apply_apply, â† LinearMap.comp_assoc, algHom_comp_mul,
    LinearMap.comp_assoc, comul_comp_algHom_adjoint]
  congr 1
  simp_rw [â† LinearMap.comp_assoc]
  congr 1
  simp_rw [TensorProduct.map_comp]

theorem schurMul_one_iff_one_schurMul_of_isReal {x y z : A â†’â‚—[â„‚] B}
  (hx : LinearMap.IsReal x) (hy : LinearMap.IsReal y) (hz : LinearMap.IsReal z) :
    x â€¢â‚› y = z â†” y â€¢â‚› x = z :=
by
  rw [LinearMap.real_inj_eq, schurMul_real, x.isReal_iff.mp hx, y.isReal_iff.mp hy,
    z.isReal_iff.mp hz]

theorem schurMul_reflexive_of_isReal {x : A â†’â‚—[â„‚] A} (hx : LinearMap.IsReal x) :
  x â€¢â‚› 1 = 1 â†” 1 â€¢â‚› x = 1 :=
schurMul_one_iff_one_schurMul_of_isReal hx LinearMap.isRealOne LinearMap.isRealOne

theorem schurMul_irreflexive_of_isReal {x : A â†’â‚—[â„‚] A} (hx : LinearMap.IsReal x) :
  x â€¢â‚› 1 = 0 â†” 1 â€¢â‚› x = 0 :=
schurMul_one_iff_one_schurMul_of_isReal hx LinearMap.isRealOne LinearMap.isRealZero
