import Monlib.LinearAlgebra.QuantumSet.Basic

local notation "lT" => LinearMap.lTensor
local notation "rT" => LinearMap.rTensor

open scoped InnerProductSpace TensorProduct

variable {Bâ‚ Bâ‚‚ : Type*} [starAlgebra Bâ‚] [starAlgebra Bâ‚‚]
  [QuantumSet Bâ‚] [QuantumSet Bâ‚‚]

noncomputable abbrev QFun.map_unit' {H : Type*}
  [NormedAddCommGroup H] [InnerProductSpace â„‚ H] [FiniteDimensional â„‚ H]
  (P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)) :=
P âˆ˜â‚— (rT _ (Algebra.linearMap â„‚ _))
  âˆ˜â‚— (TensorProduct.lid â„‚ _).symm.toLinearMap

noncomputable abbrev QFun.map_mul' {H : Type*}
  [NormedAddCommGroup H] [InnerProductSpace â„‚ H] [FiniteDimensional â„‚ H]
  (P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)) :=
(lT _ (LinearMap.mul' â„‚ Bâ‚‚))
  âˆ˜â‚— (TensorProduct.assoc â„‚ _ _ _).toLinearMap
  âˆ˜â‚— (rT _ P)
  âˆ˜â‚— (TensorProduct.assoc â„‚ _ _ _).symm.toLinearMap
  âˆ˜â‚— (lT _ P)

noncomputable abbrev QFun.map_real' {H : Type*}
  [NormedAddCommGroup H] [InnerProductSpace â„‚ H] [FiniteDimensional â„‚ H]
  (P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)) :=
(rT _ ((TensorProduct.lid â„‚ _).toLinearMap
    âˆ˜â‚— (rT _ (Coalgebra.counit âˆ˜â‚— LinearMap.mul' â„‚ _))
    âˆ˜â‚— (TensorProduct.assoc â„‚ _ _ _).symm.toLinearMap))
  âˆ˜â‚— (TensorProduct.assoc â„‚ _ _ _).symm.toLinearMap
  âˆ˜â‚— (lT Bâ‚ (((LinearMap.rTensor Bâ‚‚ (LinearMap.adjoint P)))
    âˆ˜â‚— (TensorProduct.assoc â„‚ _ _ _).symm.toLinearMap))
  âˆ˜â‚— (TensorProduct.assoc â„‚ _ _ _).toLinearMap
  âˆ˜â‚— (lT (Bâ‚ âŠ—[â„‚] H) ((Coalgebra.comul âˆ˜â‚— Algebra.linearMap â„‚ Bâ‚‚)))
  âˆ˜â‚— (TensorProduct.rid â„‚ (Bâ‚ âŠ—[â„‚] H)).symm.toLinearMap

class QFun (H : Type*)
  [NormedAddCommGroup H] [InnerProductSpace â„‚ H] [FiniteDimensional â„‚ H]
  (P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)) :
    Prop where
  map_unit : QFun.map_unit' P
    = (lT _ (Algebra.linearMap â„‚ _)) âˆ˜â‚— (TensorProduct.rid â„‚ _).symm.toLinearMap
  map_mul : QFun.map_mul' P
    = P âˆ˜â‚— (rT _ (LinearMap.mul' â„‚ Bâ‚))
        âˆ˜â‚— (TensorProduct.assoc â„‚ _ _ _).symm.toLinearMap
  map_real : QFun.map_real' P = P

lemma TensorProduct.rid_symm_adjoint {ğ•œ E : Type*} [RCLike ğ•œ] [NormedAddCommGroup E]
  [InnerProductSpace ğ•œ E] [FiniteDimensional ğ•œ E] :
    LinearMap.adjoint (TensorProduct.rid ğ•œ E).symm.toLinearMap = (TensorProduct.rid ğ•œ E).toLinearMap :=
by rw [â† LinearMap.adjoint_adjoint (TensorProduct.rid ğ•œ E).toLinearMap, TensorProduct.rid_adjoint]

variable {H : Type*}
  [NormedAddCommGroup H] [InnerProductSpace â„‚ H] [FiniteDimensional â„‚ H]

set_option maxHeartbeats 600000 in
lemma QFun.adjoint_eq
  {P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)} (hp : QFun H P) :
  LinearMap.adjoint P =
    (TensorProduct.rid â„‚ _).toLinearMap
      âˆ˜â‚— (lT _ (Coalgebra.counit âˆ˜â‚— LinearMap.mul' â„‚ _))
      âˆ˜â‚— (TensorProduct.assoc â„‚ _ _ _).symm.toLinearMap
      âˆ˜â‚— (lT _ (((TensorProduct.assoc â„‚ _ _ _).toLinearMap
        âˆ˜â‚— (rT _ P))))
      âˆ˜â‚— (TensorProduct.assoc â„‚ _ _ _).toLinearMap
      âˆ˜â‚— (rT _ ((TensorProduct.assoc â„‚ _ _ _).toLinearMap
        âˆ˜â‚— (rT _ (Coalgebra.comul âˆ˜â‚— Algebra.linearMap â„‚ _))
        âˆ˜â‚— (TensorProduct.lid â„‚ _).symm.toLinearMap)) :=
by
  simp_rw [Coalgebra.comul_eq_mul_adjoint, Coalgebra.counit_eq_unit_adjoint]
  nth_rw 1 [â† LinearMap.adjoint_adjoint (LinearMap.mul' â„‚ Bâ‚‚)]
  nth_rw 1 [â† LinearMap.adjoint_adjoint (Algebra.linearMap â„‚ Bâ‚)]
  simp_rw [â† LinearMap.adjoint_comp, â† LinearMap.lTensor_adjoint, â† LinearMap.rTensor_adjoint]
  simp_rw [â† TensorProduct.lid_adjoint, â† TensorProduct.assoc_adjoint]
  nth_rw 4 [â† TensorProduct.assoc_symm_adjoint]
  nth_rw 3 [â† TensorProduct.assoc_symm_adjoint]
  simp_rw [â† LinearMap.adjoint_comp, â† LinearMap.rTensor_adjoint]
  nth_rw 2 [â† LinearMap.adjoint_adjoint P]
  rw [â† LinearMap.rTensor_adjoint]
  nth_rw 2 [â† TensorProduct.assoc_symm_adjoint]
  simp_rw [â† LinearMap.adjoint_comp, â† LinearMap.lTensor_adjoint, â† LinearMap.adjoint_comp]
  rw [â† TensorProduct.rid_symm_adjoint]
  simp only [â† LinearMap.adjoint_comp]
  simp_rw [â† Coalgebra.comul_eq_mul_adjoint, LinearMap.comp_assoc]
  nth_rw 1 [â† hp.map_real]
  simp only [QFun.map_real', Coalgebra.counit_eq_unit_adjoint]
  congr <;> exact IsScalarTower.Algebra.ext_iff.mpr fun r â†¦ congrFun rfl

noncomputable abbrev QFun.map_counit' (P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)) :=
(TensorProduct.rid â„‚ _).toLinearMap âˆ˜â‚— (lT H Coalgebra.counit) âˆ˜â‚— P

noncomputable abbrev QFun.map_comul' (P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)) :=
(rT Bâ‚‚ P)
  âˆ˜â‚— (TensorProduct.assoc â„‚ _ _ _).symm.toLinearMap
  âˆ˜â‚— (lT Bâ‚ P)
  âˆ˜â‚— (TensorProduct.assoc â„‚ _ _ _).toLinearMap
  âˆ˜â‚— (rT H Coalgebra.comul)

class QFun.qBijective
  {P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)} (hp : QFun H P) :
    Prop where
  map_counit : QFun.map_counit' P = (TensorProduct.lid â„‚ H).toLinearMap âˆ˜â‚— (rT H Coalgebra.counit)
  map_comul : QFun.map_comul' P
    = (TensorProduct.assoc â„‚ _ _ _).symm.toLinearMap âˆ˜â‚— (lT H Coalgebra.comul) âˆ˜â‚— P


section
variable {R : Type*} [CommSemiring R]

local notation "m" => LinearMap.mul' R
local notation "Ï°" => TensorProduct.assoc R
local notation "Ï„" => TensorProduct.lid R
local notation "Ï„'" => TensorProduct.rid R
local notation x " âŠ—â‚˜ " y => TensorProduct.map x y

theorem LinearMap.comp_rid_eq_rid_comp_rTensor {M Mâ‚‚ : Type*} [AddCommMonoid M] [Module R M]
  [AddCommMonoid Mâ‚‚] [Module R Mâ‚‚] (f : M â†’â‚—[R] Mâ‚‚) :
  f âˆ˜â‚— (Ï„' M).toLinearMap = (Ï„' Mâ‚‚).toLinearMap âˆ˜â‚— (rT R f) :=
by ext; simp

theorem LinearMap.rTensor_lid_symm_comp_eq_assoc_symm_comp_lTensor_comp_lid_symm
  {Mâ‚ Mâ‚‚ Mâ‚ƒ Mâ‚„ : Type*}
  [AddCommMonoid Mâ‚] [AddCommMonoid Mâ‚‚] [AddCommMonoid Mâ‚ƒ] [AddCommMonoid Mâ‚„]
  [Module R Mâ‚] [Module R Mâ‚‚] [Module R Mâ‚ƒ] [Module R Mâ‚„]
  (f : Mâ‚ âŠ—[R] Mâ‚‚ â†’â‚—[R] Mâ‚ƒ âŠ—[R] Mâ‚„) :
  (rT Mâ‚„ (Ï„ _).symm.toLinearMap) âˆ˜â‚— f
    = (Ï° _ _ _).symm.toLinearMap âˆ˜â‚— (lT _ f) âˆ˜â‚— (Ï„ _).symm.toLinearMap :=
by
  ext a b
  obtain âŸ¨S, hSâŸ© := TensorProduct.exists_finset (R := R) (f (a âŠ—â‚œ[R] b))
  simp [hS, TensorProduct.sum_tmul, TensorProduct.tmul_sum]

theorem LinearMap.rTensor_tensor_eq_assoc_comp_rTensor_rTensor_comp_assoc_symm
  {A B C D : Type*} [AddCommMonoid A]
  [AddCommMonoid B] [AddCommMonoid C] [AddCommMonoid D]
  [Module R A] [Module R B] [Module R C] [Module R D] (x : A â†’â‚—[R] D) :
  rT (B âŠ—[R] C) x =
  (Ï° _ _ _).toLinearMap
    âˆ˜â‚— rT C (rT B x)
    âˆ˜â‚— (Ï° A B C).symm.toLinearMap :=
by
  rw [â† TensorProduct.assoc_symm_comp_rTensor, â† LinearMap.comp_assoc,
    LinearEquiv.comp_coe, LinearEquiv.symm_trans_self]
  rfl
theorem LinearMap.rTensor_rTensor_eq_assoc_symm_comp_rTensor_comp_assoc
  {A B C D : Type*} [AddCommMonoid A]
  [AddCommMonoid B] [AddCommMonoid C] [AddCommMonoid D]
  [Module R A] [Module R B] [Module R C] [Module R D] (x : A â†’â‚—[R] D) :
  rT C (rT B x) = (Ï° _ _ _).symm.toLinearMap âˆ˜â‚— (rT _ x) âˆ˜â‚— (Ï° _ _ _).toLinearMap :=
by
  rw [rTensor_tensor_eq_assoc_comp_rTensor_rTensor_comp_assoc_symm]
  ext
  simp

theorem TensorProduct.lTensor_lTensor_comp_assoc {A B C D : Type*}
  [AddCommMonoid A] [AddCommMonoid B] [AddCommMonoid C] [AddCommMonoid D]
  [Module R A] [Module R B] [Module R C] [Module R D] (x : A â†’â‚—[R] D) :
    lT B (lT C x) âˆ˜â‚— (Ï° _ _ _).toLinearMap = (Ï° _ _ _).toLinearMap âˆ˜â‚— lT _ x :=
by ext; simp

theorem LinearMap.rTensor_assoc_symm_comp_assoc_symm {A B C D : Type*} [AddCommMonoid A]
  [AddCommMonoid B] [AddCommMonoid C] [AddCommMonoid D]
  [Module R A] [Module R B] [Module R C] [Module R D] :
  rT D (Ï° A B C).symm.toLinearMap âˆ˜â‚— (Ï° A (B âŠ—[R] C) D).symm.toLinearMap
    = (Ï° (A âŠ—[R] B) C D).symm.toLinearMap âˆ˜â‚— (Ï° A B (C âŠ—[R] D)).symm.toLinearMap
      âˆ˜â‚— (lT A (Ï° B C D).toLinearMap) :=
by ext; simp

theorem rid_tensor {A B : Type*} [AddCommMonoid A] [Module R A] [AddCommMonoid B] [Module R B] :
  (Ï„' (TensorProduct R A B)).toLinearMap = lT A (Ï„' B).toLinearMap âˆ˜â‚— (Ï° A B R).toLinearMap :=
by ext; simp

theorem FrobeniusAlgebra.snake_equation_2 {A : Type*} [Semiring A] [FrobeniusAlgebra R A] :
  (Ï„' _).toLinearMap âˆ˜â‚— (lT _ (Coalgebra.counit âˆ˜â‚— LinearMap.mul' R _))
    âˆ˜â‚— (Ï° _ _ _).toLinearMap âˆ˜â‚— (rT _ (Coalgebra.comul âˆ˜â‚— Algebra.linearMap R _))
    âˆ˜â‚— (Ï„ A).symm.toLinearMap
  = 1 :=
by
  nth_rw 2 [â† LinearMap.comp_assoc]
  nth_rw 2 [â† LinearMap.comp_assoc]
  nth_rw 2 [LinearMap.comp_assoc]
  rw [lTensor_counit_mul_comp_rTensor_comul_unit]
  ext
  simp

end

local notation "Ï°" => TensorProduct.assoc â„‚
local notation "Ï„" => TensorProduct.lid â„‚
local notation "Ï„'" => TensorProduct.rid â„‚
local notation "Î·" => Algebra.linearMap â„‚

theorem QFun.self_comp_adjoint_eq_id_of_map_comul
  {P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)} (hp : QFun H P)
  (h : QFun.map_comul' P = (Ï° _ _ _).symm.toLinearMap âˆ˜â‚— (lT H Coalgebra.comul) âˆ˜â‚— P) :
  P âˆ˜â‚— LinearMap.adjoint P = 1 :=
by
  rw [QFun.adjoint_eq hp]
  simp only [â† LinearMap.comp_assoc]
  rw [LinearMap.comp_rid_eq_rid_comp_rTensor]
  simp only [LinearMap.comp_assoc, rTensor_comp_lTensor']
  rw [LinearMap.rTensor_tensor_eq_assoc_comp_rTensor_rTensor_comp_assoc_symm]
  simp only [LinearMap.rTensor_comp, LinearMap.lTensor_comp, LinearMap.comp_assoc]
  rw [â† LinearMap.comp_assoc _ _ (Ï° (Bâ‚ âŠ—[â„‚] H) _ _).symm.toLinearMap]
  rw [â† LinearMap.comp_assoc (lT Bâ‚ (rT Bâ‚‚ P) âˆ˜â‚— _ âˆ˜â‚— _ âˆ˜â‚— _ âˆ˜â‚— _),
    LinearMap.comp_assoc (lT _ _) _ _]
  rw [â† LinearMap.rTensor_assoc_symm_comp_assoc_symm]
  rw [â† LinearMap.comp_assoc _ _ (rT Bâ‚‚ (rT _ _)),
    â† LinearMap.comp_assoc _ _ (rT Bâ‚‚ (rT _ _)),
    â† LinearMap.rTensor_comp]
  rw [LinearMap.comp_assoc, â† LinearMap.comp_assoc _ _ (Ï° _ _ _).symm.toLinearMap]
  rw [â† TensorProduct.rTensor_lTensor_comp_assoc_symm,
    LinearMap.comp_assoc, â† LinearMap.comp_assoc _ _ (Ï° _ _ _).symm.toLinearMap,
    LinearEquiv.comp_coe, LinearEquiv.self_trans_symm,
    LinearEquiv.refl_toLinearMap, LinearMap.id_comp]
  simp only [LinearMap.comp_assoc, â† LinearMap.rTensor_comp]
  simp only [â† LinearMap.comp_assoc] at h âŠ¢
  rw [h]
  simp only [LinearMap.comp_assoc, hp.1]
  rw [â† LinearMap.comp_assoc _ _ (lT (H âŠ—[â„‚] Bâ‚‚) _),
    â† LinearMap.lTensor_comp, â† LinearMap.comp_assoc _ _ (lT H _),
    â† LinearMap.lTensor_comp, â† LinearMap.comp_assoc _ _ (lT _ _)]
  ext a b
  simp
  obtain âŸ¨S, hSâŸ© := TensorProduct.exists_finset (R := â„‚) (Coalgebra.comul (1 : Bâ‚‚))
  rw [hS]
  simp [TensorProduct.sum_tmul, TensorProduct.tmul_sum]
  simp_rw [TensorProduct.smul_tmul', TensorProduct.smul_tmul, â† TensorProduct.tmul_sum]
  congr
  simp_rw [â† TensorProduct.rid_tmul,
    â† LinearMap.mul'_apply (R := â„‚) (A := Bâ‚‚), â† LinearMap.comp_apply,
    â† LinearMap.lTensor_tmul, â† TensorProduct.assoc_tmul,
    â† LinearEquiv.coe_toLinearMap, â† map_sum]
  rw [â† TensorProduct.sum_tmul, â† hS]
  have := @FrobeniusAlgebra.lTensor_counit_mul_comp_rTensor_comul_unit (R := â„‚) (A := Bâ‚‚)
    _ _ (QuantumSet.isFrobeniusAlgebra (A := Bâ‚‚))
  simp only [TensorProduct.ext_iff', LinearMap.comp_apply, LinearMap.rTensor_tmul,
    LinearEquiv.coe_coe, TensorProduct.comm_tmul, LinearMap.lTensor_tmul,
    Algebra.linearMap_apply, Algebra.algebraMap_eq_smul_one] at this
  specialize this 1
  simp only [map_smul, one_smul] at this
  simp [this]

set_option maxHeartbeats 300000 in
theorem QFun.adjoint_comp_self_eq_id_of_map_counit
  {P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)} (hp : QFun H P)
  (h : QFun.map_counit' P = (Ï„ H).toLinearMap âˆ˜â‚— (rT H Coalgebra.counit)) :
  LinearMap.adjoint P âˆ˜â‚— P = 1 :=
by
  rw [QFun.adjoint_eq hp]
  simp only [LinearMap.rTensor_comp, LinearMap.lTensor_comp, LinearMap.comp_assoc]
  rw [LinearMap.rTensor_lid_symm_comp_eq_assoc_symm_comp_lTensor_comp_lid_symm,
    â† LinearMap.comp_assoc (_ âˆ˜â‚— (Ï„ _).symm.toLinearMap) _ (rT _ (rT _ _)),
    â† TensorProduct.assoc_symm_comp_rTensor]
  simp only [LinearMap.comp_assoc]
  rw [â† LinearMap.comp_assoc _ (Ï° Bâ‚ H Bâ‚‚).symm.toLinearMap,
    â† TensorProduct.assoc_symm_comp_rTensor,
    â† LinearMap.comp_assoc _ _ (rT _ (Î· Bâ‚)),
    LinearMap.rTensor_comp_lTensor, â† LinearMap.lTensor_comp_rTensor]
  simp only [LinearMap.comp_assoc]
  rw [â† LinearMap.comp_assoc (_ âˆ˜â‚— _) (lT Bâ‚ P) (rT (H âŠ—[â„‚] Bâ‚‚) _),
    LinearMap.rTensor_comp_lTensor, â† LinearMap.lTensor_comp_rTensor]
  have :
    (Ï° Bâ‚ (Bâ‚ âŠ—[â„‚] H) Bâ‚‚).toLinearMap
        âˆ˜â‚— rT Bâ‚‚ (Ï° Bâ‚ Bâ‚ H).toLinearMap
        âˆ˜â‚— (Ï° (Bâ‚ âŠ—[â„‚] Bâ‚) H Bâ‚‚).symm.toLinearMap
    = lT _ (Ï° _ _ _).symm.toLinearMap âˆ˜â‚— (Ï° _ _ _).toLinearMap :=
  by
    apply TensorProduct.ext_fourfold'
    intro a b c d
    simp
  calc (Ï„' (Bâ‚ âŠ—[â„‚] H)).toLinearMap âˆ˜â‚— lT (Bâ‚ âŠ—[â„‚] H) Coalgebra.counit
        âˆ˜â‚— lT (Bâ‚ âŠ—[â„‚] H) (LinearMap.mul' â„‚ Bâ‚‚)
        âˆ˜â‚— (Ï° Bâ‚ H (Bâ‚‚ âŠ—[â„‚] Bâ‚‚)).symm.toLinearMap
        âˆ˜â‚— lT Bâ‚ (Ï° H Bâ‚‚ Bâ‚‚).toLinearMap
        âˆ˜â‚— lT Bâ‚ (rT Bâ‚‚ P)
        âˆ˜â‚— (Ï° Bâ‚ (Bâ‚ âŠ—[â„‚] H) Bâ‚‚).toLinearMap
        âˆ˜â‚— rT Bâ‚‚ (Ï° Bâ‚ Bâ‚ H).toLinearMap
        âˆ˜â‚— (Ï° (Bâ‚ âŠ—[â„‚] Bâ‚) H Bâ‚‚).symm.toLinearMap
        âˆ˜â‚— lT (Bâ‚ âŠ—[â„‚] Bâ‚) P
        âˆ˜â‚— rT (Bâ‚ âŠ—[â„‚] H) Coalgebra.comul
        âˆ˜â‚— rT (Bâ‚ âŠ—[â„‚] H) (Î· Bâ‚) âˆ˜â‚— (Ï„ (Bâ‚ âŠ—[â„‚] H)).symm.toLinearMap
      = (Ï„' (Bâ‚ âŠ—[â„‚] H)).toLinearMap âˆ˜â‚— lT (Bâ‚ âŠ—[â„‚] H) Coalgebra.counit
        âˆ˜â‚— ((Ï° _ _ _).symm.toLinearMap âˆ˜â‚— lT _ (lT _ (LinearMap.mul' â„‚ Bâ‚‚)))
          âˆ˜â‚— (lT _ ((Ï° H _ _).toLinearMap âˆ˜â‚— rT Bâ‚‚ P))
          âˆ˜â‚— (lT _ (Ï° _ _ _).symm.toLinearMap âˆ˜â‚— (Ï° _ _ _).toLinearMap)
          âˆ˜â‚— lT _ P
        âˆ˜â‚— rT (Bâ‚ âŠ—[â„‚] H) Coalgebra.comul
        âˆ˜â‚— rT (Bâ‚ âŠ—[â„‚] H) (Î· Bâ‚) âˆ˜â‚— (Ï„ (Bâ‚ âŠ—[â„‚] H)).symm.toLinearMap :=
      by
        rw [TensorProduct.assoc_symm_comp_lTensor_lTensor, â† this]
        simp only [LinearMap.lTensor_comp, LinearMap.rTensor_comp, LinearMap.comp_assoc]
    _ = (Ï„' (Bâ‚ âŠ—[â„‚] H)).toLinearMap âˆ˜â‚— lT (Bâ‚ âŠ—[â„‚] H) Coalgebra.counit
        âˆ˜â‚— (Ï° _ _ _).symm.toLinearMap
        âˆ˜â‚— lT _ (lT _ (LinearMap.mul' â„‚ Bâ‚‚) âˆ˜â‚— (Ï° _ _ _).toLinearMap âˆ˜â‚— rT _ P
          âˆ˜â‚— (Ï° _ _ _).symm.toLinearMap)
        âˆ˜â‚— (lT _ (lT _ P) âˆ˜â‚— (Ï° _ _ _).toLinearMap)
        âˆ˜â‚— rT (Bâ‚ âŠ—[â„‚] H) Coalgebra.comul
        âˆ˜â‚— rT (Bâ‚ âŠ—[â„‚] H) (Î· Bâ‚) âˆ˜â‚— (Ï„ (Bâ‚ âŠ—[â„‚] H)).symm.toLinearMap :=
      by
        rw [TensorProduct.lTensor_lTensor_comp_assoc]
        simp only [LinearMap.lTensor_comp, LinearMap.rTensor_comp, LinearMap.comp_assoc]
    _ = (Ï„' (Bâ‚ âŠ—[â„‚] H)).toLinearMap âˆ˜â‚— lT (Bâ‚ âŠ—[â„‚] H) Coalgebra.counit
        âˆ˜â‚— (Ï° _ _ _).symm.toLinearMap
        âˆ˜â‚— lT _ (lT _ (LinearMap.mul' â„‚ Bâ‚‚) âˆ˜â‚— (Ï° _ _ _).toLinearMap âˆ˜â‚— rT _ P
          âˆ˜â‚— (Ï° _ _ _).symm.toLinearMap âˆ˜â‚— lT _ P)
        âˆ˜â‚— (Ï° _ _ _).toLinearMap
        âˆ˜â‚— rT (Bâ‚ âŠ—[â„‚] H) Coalgebra.comul
        âˆ˜â‚— rT (Bâ‚ âŠ—[â„‚] H) (Î· Bâ‚) âˆ˜â‚— (Ï„ (Bâ‚ âŠ—[â„‚] H)).symm.toLinearMap :=
      by
        simp only [LinearMap.lTensor_comp, LinearMap.comp_assoc]
    _ = (Ï„' (Bâ‚ âŠ—[â„‚] H)).toLinearMap âˆ˜â‚— (lT (Bâ‚ âŠ—[â„‚] H) Coalgebra.counit
        âˆ˜â‚— (Ï° _ _ _).symm.toLinearMap)
        âˆ˜â‚— lT _ (P âˆ˜â‚— (rT _ (LinearMap.mul' â„‚ Bâ‚)) âˆ˜â‚— (Ï° _ _ _).symm.toLinearMap)
        âˆ˜â‚— (Ï° _ _ _).toLinearMap
        âˆ˜â‚— rT (Bâ‚ âŠ—[â„‚] H) Coalgebra.comul
        âˆ˜â‚— rT (Bâ‚ âŠ—[â„‚] H) (Î· Bâ‚) âˆ˜â‚— (Ï„ (Bâ‚ âŠ—[â„‚] H)).symm.toLinearMap :=
      by
        simp only [hp.map_mul, LinearMap.comp_assoc]
    _ = lT _ (Ï„' _).toLinearMap âˆ˜â‚— ((Ï° _ _ _).toLinearMap âˆ˜â‚— (Ï° _ _ _).symm.toLinearMap)
        âˆ˜â‚— lT _ (lT _ Coalgebra.counit âˆ˜â‚— P âˆ˜â‚— (rT _ (LinearMap.mul' â„‚ Bâ‚)) âˆ˜â‚— (Ï° _ _ _).symm.toLinearMap)
        âˆ˜â‚— (Ï° _ _ _).toLinearMap
        âˆ˜â‚— rT (Bâ‚ âŠ—[â„‚] H) (Coalgebra.comul âˆ˜â‚— Î· Bâ‚)
        âˆ˜â‚— (Ï„ (Bâ‚ âŠ—[â„‚] H)).symm.toLinearMap :=
      by
        simp only [â† TensorProduct.assoc_symm_comp_lTensor_lTensor, rid_tensor,
          LinearMap.lTensor_comp, LinearMap.rTensor_comp, LinearMap.comp_assoc]
    _ = lT _ (((Ï„' _).toLinearMap âˆ˜â‚— lT _ Coalgebra.counit âˆ˜â‚— P)
          âˆ˜â‚— (rT _ (LinearMap.mul' â„‚ Bâ‚)) âˆ˜â‚— (Ï° _ _ _).symm.toLinearMap)
        âˆ˜â‚— (Ï° _ _ _).toLinearMap
        âˆ˜â‚— rT (Bâ‚ âŠ—[â„‚] H) (Coalgebra.comul âˆ˜â‚— Î· Bâ‚)
        âˆ˜â‚— (Ï„ (Bâ‚ âŠ—[â„‚] H)).symm.toLinearMap :=
      by
        simp only [LinearEquiv.comp_coe, LinearEquiv.symm_trans_self,
          LinearEquiv.refl_toLinearMap, LinearMap.id_comp, LinearMap.lTensor_comp,
          LinearMap.comp_assoc]
    _ = lT _ (((Ï„ H).toLinearMap âˆ˜â‚— (rT H Coalgebra.counit))
          âˆ˜â‚— (rT _ (LinearMap.mul' â„‚ Bâ‚)) âˆ˜â‚— (Ï° _ _ _).symm.toLinearMap)
        âˆ˜â‚— (Ï° _ _ _).toLinearMap
        âˆ˜â‚— rT (Bâ‚ âŠ—[â„‚] H) (Coalgebra.comul âˆ˜â‚— Î· Bâ‚)
        âˆ˜â‚— (Ï„ (Bâ‚ âŠ—[â„‚] H)).symm.toLinearMap :=
      by simp [h]
    _ = rT H ((Ï„' _).toLinearMap âˆ˜â‚— (lT _ (Coalgebra.counit âˆ˜â‚— LinearMap.mul' â„‚ _))
        âˆ˜â‚— (Ï° _ _ _).toLinearMap âˆ˜â‚— (rT _ (Coalgebra.comul âˆ˜â‚— Î· _))
        âˆ˜â‚— (Ï„ _).symm.toLinearMap) :=
      by
        ext
        obtain âŸ¨S, hSâŸ© := TensorProduct.exists_finset (R := â„‚) (Coalgebra.comul (1 : Bâ‚))
        simp
        rw [hS]
        simp [TensorProduct.sum_tmul, map_sum, TensorProduct.smul_tmul]
    _ = rT H 1 :=
      by rw [@FrobeniusAlgebra.snake_equation_2 â„‚ _ Bâ‚ _
        (QuantumSet.isFrobeniusAlgebra (A := Bâ‚))]
    _ = 1 := by ext; simp

theorem QFun.map_counit_of_adjoint_comp_self_eq_id
  {P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)} (hp : QFun H P) (h : LinearMap.adjoint P âˆ˜â‚— P = 1) :
  map_counit' P = (Ï„ H).toLinearMap âˆ˜â‚— (rT H Coalgebra.counit) :=
by
  have :=
    calc LinearMap.adjoint P âˆ˜â‚— (lT H (Î· Bâ‚‚) âˆ˜â‚— (Ï„' H).symm.toLinearMap)
          = LinearMap.adjoint P âˆ˜â‚— (P âˆ˜â‚— rT H (Î· _) âˆ˜â‚— (Ï„ H).symm.toLinearMap) :=
            by rw [â† hp.map_unit]
        _ = rT H (Î· _) âˆ˜â‚— (Ï„ H).symm.toLinearMap :=
            by rw [â† LinearMap.comp_assoc, h, LinearMap.one_comp]
  apply_fun LinearMap.adjoint at this
  simp only [LinearMap.adjoint_comp, LinearMap.lTensor_adjoint, LinearMap.rTensor_adjoint,
    â† TensorProduct.rid_adjoint, Coalgebra.counit_eq_unit_adjoint, LinearMap.adjoint_adjoint,
    â† TensorProduct.lid_adjoint, LinearMap.comp_assoc] at this
  rw [â† sub_eq_zero] at this âŠ¢
  simp only [map_counit', Coalgebra.counit_eq_unit_adjoint]
  simp [â† this]
  congr <;> ext <;> simp

lemma LinearMap.lTensor_one {R Mâ‚ Mâ‚‚ : Type*} [CommSemiring R] [AddCommMonoid Mâ‚] [AddCommMonoid Mâ‚‚]
  [Module R Mâ‚] [Module R Mâ‚‚] :
  lT Mâ‚ (1 : Mâ‚‚ â†’â‚—[R] Mâ‚‚) = 1 :=
by ext; simp
lemma LinearMap.rTensor_one {R Mâ‚ Mâ‚‚ : Type*} [CommSemiring R] [AddCommMonoid Mâ‚] [AddCommMonoid Mâ‚‚]
  [Module R Mâ‚] [Module R Mâ‚‚] :
  rT Mâ‚ (1 : Mâ‚‚ â†’â‚—[R] Mâ‚‚) = 1 :=
by ext; simp

set_option maxHeartbeats 300000 in
theorem QFun.map_comul_of_inv_eq_adjoint
  {P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)} (hp : QFun H P) (hâ‚ : P âˆ˜â‚— LinearMap.adjoint P = 1)
  (hâ‚‚ : LinearMap.adjoint P âˆ˜â‚— P = 1) :
    map_comul' P = (Ï° _ _ _).symm.toLinearMap âˆ˜â‚— (lT H Coalgebra.comul) âˆ˜â‚— P :=
by
  have : LinearMap.adjoint P âˆ˜â‚— map_mul' P âˆ˜â‚— (lT Bâ‚ (LinearMap.adjoint P))
    âˆ˜â‚— (Ï° _ _ _).toLinearMap âˆ˜â‚— (rT Bâ‚‚ (LinearMap.adjoint P)) = LinearMap.adjoint (map_comul' P) :=
  by
    rw [hp.map_mul]
    simp_rw [â† LinearMap.comp_assoc, hâ‚‚, LinearMap.one_comp, â† LinearMap.lTensor_adjoint,
      â† LinearMap.rTensor_adjoint, Coalgebra.comul_eq_mul_adjoint]
    rw [â† TensorProduct.assoc_adjoint]
    nth_rw 2 [â† TensorProduct.assoc_symm_adjoint]
    nth_rw 1 [â† LinearMap.adjoint_adjoint (LinearMap.mul' â„‚ Bâ‚)]
    rw [â† LinearMap.rTensor_adjoint]
    simp only [â† LinearMap.adjoint_comp, LinearMap.comp_assoc]
  simp_all [map_mul', LinearMap.comp_assoc]
  rw [â† LinearMap.comp_assoc _ _ (lT Bâ‚ P), â† LinearMap.lTensor_comp, hâ‚, LinearMap.lTensor_one,
    LinearMap.one_comp, â† LinearMap.comp_assoc _ _ (Ï° _ _ _).symm.toLinearMap,
    LinearEquiv.comp_coe, LinearEquiv.self_trans_symm] at this
  simp only [LinearEquiv.refl_toLinearMap] at this
  rw [LinearMap.id_comp, â† LinearMap.rTensor_comp, hâ‚, LinearMap.rTensor_one, LinearMap.comp_one] at this
  apply_fun LinearMap.adjoint at this
  simp only [LinearMap.adjoint_comp, LinearMap.adjoint_adjoint] at this
  rw [â† this, â† TensorProduct.assoc_adjoint, Coalgebra.comul_eq_mul_adjoint,
    â† LinearMap.lTensor_adjoint, â† LinearMap.comp_assoc, â† LinearMap.adjoint_comp]

theorem QFun.qBijective_iff_inv_eq_adjoint
  {P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)} (hp : QFun H P) :
  hp.qBijective â†” P âˆ˜â‚— LinearMap.adjoint P = 1 âˆ§ LinearMap.adjoint P âˆ˜â‚— P = 1 :=
âŸ¨Î» h => âŸ¨hp.self_comp_adjoint_eq_id_of_map_comul h.2,
  hp.adjoint_comp_self_eq_id_of_map_counit h.1âŸ©,
  Î» âŸ¨h1, h2âŸ© => âŸ¨hp.map_counit_of_adjoint_comp_self_eq_id h2,
  hp.map_comul_of_inv_eq_adjoint h1 h2âŸ©âŸ©

noncomputable def QFun.qBijective.toLinearEquiv
  {P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)} [hp : QFun H P]
  (h : hp.qBijective) :
    (Bâ‚ âŠ—[â„‚] H) â‰ƒâ‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚) where
  toLinearMap := P
  invFun := LinearMap.adjoint P
  left_inv _ := by
    simp only [LinearMap.toFun_eq_coe, â† LinearMap.comp_apply]
    rw [(hp.qBijective_iff_inv_eq_adjoint.mp h).2, LinearMap.one_apply]
  right_inv _ := by
    simp only [LinearMap.toFun_eq_coe, â† LinearMap.comp_apply]
    rw [(hp.qBijective_iff_inv_eq_adjoint.mp h).1, LinearMap.one_apply]

lemma QFun.qBijective.toLinearEquiv_toLinearMap
  {P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)} [hp : QFun H P]
  (h : hp.qBijective) :
    h.toLinearEquiv.toLinearMap = P :=
rfl

lemma QFun.qBijective.toLinearEquiv_symm_toLinearMap
  {P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)} [hp : QFun H P]
  (h : hp.qBijective) :
    h.toLinearEquiv.symm.toLinearMap = LinearMap.adjoint P :=
rfl

theorem QFun.qBijective_iso_id
  {P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)} [hp : QFun H P] (h : hp.qBijective) :
    h.toLinearEquiv.toLinearMap âˆ˜â‚—
      (rT _ 1) âˆ˜â‚— h.toLinearEquiv.symm.toLinearMap = lT _ 1 :=
by
  ext
  simp [LinearMap.rTensor_one, LinearMap.lTensor_one]

theorem rankOne_one_one_eq :
  ContinuousLinearMap.toLinearMap (rankOne â„‚ (1 : Bâ‚) (1 : Bâ‚‚)) = Î· Bâ‚ âˆ˜â‚— Coalgebra.counit :=
by
  rw [Coalgebra.counit_eq_bra_one]
  ext
  simp [Algebra.algebraMap_eq_smul_one]

lemma QFun.map_unit'' {P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)} (hp : QFun H P) :
  P âˆ˜â‚— rT H (Î· Bâ‚) = lT H (Î· Bâ‚‚) âˆ˜â‚— (TensorProduct.comm â„‚ _ _).toLinearMap :=
calc P âˆ˜â‚— rT H (Î· Bâ‚) = lT H (Î· Bâ‚‚) âˆ˜â‚— ((Ï„' _).symm.toLinearMap âˆ˜â‚— (Ï„ _).toLinearMap) :=
    by
      rw [â† LinearMap.comp_assoc, â† hp.map_unit, map_unit']
      simp only [LinearMap.comp_assoc, LinearEquiv.comp_coe, LinearEquiv.self_trans_symm]
      rfl
  _ = lT H (Î· Bâ‚‚) âˆ˜â‚— (TensorProduct.comm â„‚ _ _).toLinearMap := by ext; simp

lemma QFun.counit_map_adjoint {P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)} (hp : QFun H P) :
  (rT _ Coalgebra.counit) âˆ˜â‚— LinearMap.adjoint P
    = (TensorProduct.comm â„‚ _ _).symm.toLinearMap âˆ˜â‚— lT _ Coalgebra.counit :=
calc (rT _ Coalgebra.counit) âˆ˜â‚— LinearMap.adjoint P
    = LinearMap.adjoint (P âˆ˜â‚— (rT _ (Î· Bâ‚))) :=
      by
        rw [Coalgebra.counit_eq_unit_adjoint, â† LinearMap.rTensor_adjoint,
          LinearMap.adjoint_comp]
        congr; ext; rfl
  _ = LinearMap.adjoint (lT H (Î· Bâ‚‚) âˆ˜â‚— (TensorProduct.comm â„‚ _ _).toLinearMap) :=
      by rw [hp.map_unit'']
  _ = (TensorProduct.comm â„‚ _ _).symm.toLinearMap âˆ˜â‚— lT _ Coalgebra.counit :=
      by
        rw [LinearMap.adjoint_comp, LinearMap.lTensor_adjoint,
          Coalgebra.counit_eq_unit_adjoint, TensorProduct.comm_adjoint]
        congr; ext; rfl

/-- for any `qBijective` function `P`,
  we get `P âˆ˜ (|1âŸ©âŸ¨1| âŠ— id) âˆ˜ adjoint P = (id âŠ— |1âŸ©âŸ¨1|)`. -/
theorem QFun.qBijective_iso_rankOne_one_one
  {P : (Bâ‚ âŠ—[â„‚] H) â†’â‚—[â„‚] (H âŠ—[â„‚] Bâ‚‚)} [hp : QFun H P] (h : hp.qBijective) :
    h.toLinearEquiv.toLinearMap âˆ˜â‚— (rT _ (rankOne â„‚ (1 : Bâ‚) (1 : Bâ‚))) âˆ˜â‚— h.toLinearEquiv.symm.toLinearMap
      = lT _ (rankOne â„‚ (1 : Bâ‚‚) (1 : Bâ‚‚)) :=
by
  rw [rankOne_one_one_eq, LinearMap.rTensor_comp,
    h.toLinearEquiv_toLinearMap, h.toLinearEquiv_symm_toLinearMap,
    LinearMap.comp_assoc, hp.counit_map_adjoint, â† LinearMap.comp_assoc, hp.map_unit'']
  nth_rw 1 [LinearMap.comp_assoc]
  nth_rw 2 [â† LinearMap.comp_assoc]
  rw [LinearEquiv.comp_coe, LinearEquiv.symm_trans_self, LinearEquiv.refl_toLinearMap,
    LinearMap.id_comp, â† LinearMap.lTensor_comp, rankOne_one_one_eq]
