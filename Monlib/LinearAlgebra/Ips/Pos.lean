/-
Copyright (c) 2023 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
import Mathlib.Analysis.InnerProductSpace.Adjoint
import Mathlib.Analysis.InnerProductSpace.Spectrum
import Monlib.LinearAlgebra.Ips.RankOne
import Monlib.LinearAlgebra.End
import Mathlib.Analysis.InnerProductSpace.Positive
import Monlib.Preq.RCLikeLe

#align_import linear_algebra.my_ips.pos

/-!

# Positive linear maps

This file generalises the notion of positivity to linear maps. We follow the same definition as `continuous_linear_map.isPositive` but change the `self-adjoinnt` property to `is_symmertric`, i.e., a linear map is positive if it is symmetric and `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`

## Main statements

for linear maps:
* `linear_map.isPositive.conj_adjoint` : if `T : E â†’â‚—[ğ•œ] E` and `E` is a finite-dimensional space,
  then for any `S : E â†’â‚—[ğ•œ] F`, we have `S.comp (T.comp S.adjoint)` is also positive.

-/


/-- set over `K` is **non-negative** if all its elements are non-negative -/
def Set.IsNonneg {K : Type _} [LE K] [Zero K] (A : Set K) : Prop :=
  âˆ€ x : K, x âˆˆ A â†’ 0 â‰¤ x

open InnerProductSpace RCLike

open scoped InnerProduct ComplexConjugate

variable {ğ•œ E F : Type _} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]
  [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ F]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

namespace LinearMap

/-- `T` is (semi-definite) **positive** if `T` is symmetric
and `âˆ€ x : V, 0 â‰¤ re âŸªx, T xâŸ«` -/
def IsPositive (T : E â†’â‚—[ğ•œ] E) : Prop :=
  T.IsSymmetric âˆ§ âˆ€ x : E, 0 â‰¤ re âŸªx, T xâŸ«

theorem isPositiveZero : (0 : E â†’â‚—[ğ•œ] E).IsPositive :=
  by
  refine' âŸ¨isSymmetric_zero, fun x => _âŸ©
  simp_rw [zero_apply, inner_re_zero_right, le_rfl]

theorem isPositiveOne : (1 : E â†’â‚—[ğ•œ] E).IsPositive :=
  âŸ¨isSymmetric_id, fun _ => inner_self_nonnegâŸ©

theorem IsPositive.add {S T : E â†’â‚—[ğ•œ] E} (hS : S.IsPositive) (hT : T.IsPositive) :
    (S + T).IsPositive :=
  by
  refine' âŸ¨IsSymmetric.add hS.1 hT.1, fun x => _âŸ©
  rw [add_apply, inner_add_right, map_add]
  exact add_nonneg (hS.2 _) (hT.2 _)

theorem IsPositive.inner_nonneg_left {T : E â†’â‚—[ğ•œ] E} (hT : IsPositive T) (x : E) :
    0 â‰¤ re âŸªT x, xâŸ« := by rw [inner_re_symm]; exact hT.2 x

theorem IsPositive.inner_nonneg_right {T : E â†’â‚—[ğ•œ] E} (hT : IsPositive T) (x : E) :
    0 â‰¤ re âŸªx, T xâŸ« :=
  hT.2 x

/-- a linear projection onto `U` along its complement `V` is positive if
and only if `U` and `V` are pairwise orthogonal -/
theorem linear_proj_isPositive_iff {U V : Submodule ğ•œ E} (hUV : IsCompl U V) :
    (U.subtype.comp (U.linearProjOfIsCompl V hUV)).IsPositive â†” U âŸ‚ V :=
  by
  constructor
  Â· intro h u hu v hv
    rw [â† Subtype.coe_mk u hu, â† Subtype.coe_mk v hv, â†
      Submodule.linearProjOfIsCompl_apply_left hUV âŸ¨u, huâŸ©, â† Submodule.subtype_apply U, â†
      comp_apply, â† h.1 _ _, comp_apply, Submodule.linearProjOfIsCompl_apply_right hUV âŸ¨v, hvâŸ©,
      map_zero, inner_zero_left]
  Â· intro h
    have : (U.subtype.comp (U.linearProjOfIsCompl V hUV)).IsSymmetric :=
      by
      intro x y
      nth_rw 1 [â† Submodule.linear_proj_add_linearProjOfIsCompl_eq_self hUV y]
      nth_rw 2 [â† Submodule.linear_proj_add_linearProjOfIsCompl_eq_self hUV x]
      rw [Submodule.isOrtho_iff_inner_eq] at h
      simp_rw [inner_add_right, inner_add_left, comp_apply, Submodule.subtype_apply _]
      rw [@h _ (SetLike.coe_mem _) _ (SetLike.coe_mem _),
        inner_eq_zero_symm.mp (h _ (SetLike.coe_mem _) _ (SetLike.coe_mem _))]
    refine' âŸ¨this, _âŸ©
    intro x
    rw [comp_apply, Submodule.subtype_apply, â† Submodule.linearProjOfIsCompl_idempotent, â†
      Submodule.subtype_apply, â† comp_apply, â† this _ ((U.linearProjOfIsCompl V hUV) x)]
    exact inner_self_nonneg

section FiniteDimensional

local notation "e" => IsSymmetric.eigenvectorBasis

local notation "Î±" => IsSymmetric.eigenvalues

local notation "âˆš" => Real.sqrt

variable [FiniteDimensional ğ•œ E] (T : E â†’â‚—[ğ•œ] E)

open scoped ComplexOrder

/-- the spectrum of a positive linear map is non-negative -/
theorem IsPositive.nonneg_spectrum (h : T.IsPositive) : (spectrum ğ•œ T).IsNonneg :=
  by
  cases' h with hT h
  intro Î¼ hÎ¼
  simp_rw [â† Module.End.hasEigenvalue_iff_mem_spectrum] at hÎ¼
  have : â†‘(re Î¼) = Î¼ := by
    simp_rw [â† conj_eq_iff_re]
    exact IsSymmetric.conj_eigenvalue_eq_self hT hÎ¼
  rw [â† this] at hÎ¼
  rw [RCLike.nonneg_def']
  exact âŸ¨this, eigenvalue_nonneg_of_nonneg hÎ¼ hâŸ©

open scoped BigOperators

/-- given a symmetric linear map with a non-negative spectrum,
we can write `T x = âˆ‘ i, âˆšÎ± i â€¢ âˆšÎ± i â€¢ âŸªe i, xâŸ«` for any `x âˆˆ E`,
where `Î± i` are the eigenvalues of `T` and `e i` are the respective eigenvectors
that form an eigenbasis (`isSymmetric.eigenvector_basis`) -/
theorem sq_mul_sq_eq_self_of_isSymmetric_and_nonneg_spectrum
    (hT : T.IsSymmetric) (hT1 : (spectrum ğ•œ T).IsNonneg)
    (v : E) : T v = âˆ‘ i, (âˆš (Î± hT rfl i) â€¢ âˆš (Î± hT rfl i) : ğ•œ) â€¢ âŸªe hT rfl i, vâŸ« â€¢ e hT rfl i :=
  by
  have : âˆ€ i, 0 â‰¤ Î± hT rfl i := fun i =>
    by
    specialize hT1 (hT.eigenvalues rfl i)
    simp only [zero_le_real, ofReal_re, true_and_iff] at hT1
    apply
      hT1
        (Module.End.hasEigenvalue_iff_mem_spectrum.mp (hT.hasEigenvalue_eigenvalues rfl i))
  calc
    T v = âˆ‘ i, âŸªe hT rfl i, vâŸ« â€¢ T (e hT rfl i) := by
      simp_rw [â† OrthonormalBasis.repr_apply_apply, â† map_smul_of_tower, â† map_sum,
        OrthonormalBasis.sum_repr (e hT rfl) v]
    _ = âˆ‘ i, (âˆš (Î± hT rfl i) â€¢ âˆš (Î± hT rfl i) : ğ•œ) â€¢ âŸªe hT rfl i, vâŸ« â€¢ e hT rfl i := by
      simp_rw [IsSymmetric.apply_eigenvectorBasis, smul_smul,
        real_smul_ofReal, â† ofReal_mul, â† Real.sqrt_mul (this _), Real.sqrt_mul_self (this _),
        mul_comm]

/-- given a symmetric linear map `T` and a real number `r`,
we can define a linear map `S` such that `S = T ^ r` -/
noncomputable def rePow
    (hT : T.IsSymmetric) (r : â„) : E â†’â‚—[ğ•œ] E
    where
  toFun v := âˆ‘ i, (((Î± hT rfl i : â„) ^ r : â„) : ğ•œ) â€¢ âŸªe hT rfl i, vâŸ« â€¢ e hT rfl i
  map_add' x y := by simp_rw [inner_add_right, add_smul, smul_add, Finset.sum_add_distrib]
  map_smul' r x := by
    simp_rw [inner_smul_right, â† smul_smul, Finset.smul_sum, RingHom.id_apply, smul_smul, â†
      mul_assoc, mul_comm]

section

noncomputable def cpow [InnerProductSpace â„‚ E] [FiniteDimensional â„‚ E]
    (T : E â†’â‚—[â„‚] E) (hT : T.IsPositive) (c : â„‚) : E â†’â‚—[â„‚] E
    where
  toFun v := âˆ‘ i, (Î± hT.1 rfl i ^ c : â„‚) â€¢ âŸªe hT.1 rfl i, vâŸ«_â„‚ â€¢ e hT.1 rfl i
  map_add' x y := by simp_rw [inner_add_right, add_smul, smul_add, Finset.sum_add_distrib]
  map_smul' r x := by
    simp_rw [inner_smul_right, â† smul_smul, Finset.smul_sum, RingHom.id_apply, smul_smul, â†
      mul_assoc, mul_comm]

theorem cpow_apply [InnerProductSpace â„‚ E] [FiniteDimensional â„‚ E]
    (T : E â†’â‚—[â„‚] E) (hT : T.IsPositive) (c : â„‚) (v : E) :
    T.cpow hT c v = âˆ‘ i, (Î± hT.1 rfl i ^ c : â„‚) â€¢ âŸªe hT.1 rfl i, vâŸ«_â„‚ â€¢ e hT.1 rfl i :=
  rfl

end

theorem rePow_apply (hT : T.IsSymmetric)
    (r : â„) (v : E) :
    T.rePow hT r v = âˆ‘ i, (((Î± hT rfl i : â„) ^ r : â„) : ğ•œ) â€¢ âŸªe hT rfl i, vâŸ« â€¢ e hT rfl i :=
  rfl

/-- the square root of a symmetric linear map can then directly be defined with `re_pow` -/
noncomputable def sqrt
    (h : T.IsSymmetric) : E â†’â‚—[ğ•œ] E :=
  T.rePow h (1 / 2 : â„)

/-- the square root of a symmetric linear map `T`
is written as `T x = âˆ‘ i, âˆš (Î± i) â€¢ âŸªe i, xâŸ« â€¢ e i` for any `x âˆˆ E`,
where `Î± i` are the eigenvalues of `T` and `e i` are the respective eigenvectors
that form an eigenbasis (`isSymmetric.eigenvector_basis`) -/
theorem sqrt_apply (hT : T.IsSymmetric)
    (x : E) : T.sqrt hT x = âˆ‘ i, (âˆš (Î± hT rfl i) : ğ•œ) â€¢ âŸªe hT rfl i, xâŸ« â€¢ e hT rfl i := by
  simp_rw [Real.sqrt_eq_rpow _]; rfl

/-- given a symmetric linear map `T` with a non-negative spectrum,
the square root of `T` composed with itself equals itself, i.e., `T.sqrt ^ 2 = T`  -/
theorem sqrt_sq_eq_self_of_isSymmetric_and_nonneg_spectrum
  (hT : T.IsSymmetric) (hT1 : (spectrum ğ•œ T).IsNonneg) :
    T.sqrt hT ^ 2 = T := by
  simp_rw [pow_two, mul_eq_comp, LinearMap.ext_iff, comp_apply, sqrt_apply, inner_sum,
    inner_smul_real_right, smul_smul, inner_smul_right, â† OrthonormalBasis.repr_apply_apply,
    OrthonormalBasis.repr_self, EuclideanSpace.single_apply, mul_boole, smul_ite, smul_zero,
    Finset.sum_ite_eq, Finset.mem_univ, if_true, Algebra.mul_smul_comm,
    sq_mul_sq_eq_self_of_isSymmetric_and_nonneg_spectrum T hT hT1,
    OrthonormalBasis.repr_apply_apply, â† smul_eq_mul, â† smul_assoc, forall_const]

/-- given a symmetric linear map `T`, we have that its root is positive -/
theorem IsSymmetric.sqrtIsPositive
    (hT : T.IsSymmetric) : (T.sqrt hT).IsPositive :=
  by
  have : (T.sqrt hT).IsSymmetric := by
    intro x y
    simp_rw [sqrt_apply T hT, inner_sum, sum_inner, smul_smul, inner_smul_right, inner_smul_left]
    have : âˆ€ i, conj (âˆš (Î± hT rfl i) : ğ•œ) = (âˆš (Î± hT rfl i) : ğ•œ) := fun i => by
      simp_rw [conj_eq_iff_re, ofReal_re]
    simp_rw [mul_assoc, map_mul, this _, inner_conj_symm, mul_comm âŸªe hT rfl _, yâŸ« _, â† mul_assoc]
  refine' âŸ¨this, _âŸ©
  intro x
  simp_rw [sqrt_apply _ hT, inner_sum, map_sum, inner_smul_right]
  apply Finset.sum_nonneg'
  intro i
  simp_rw [â† inner_conj_symm x _, â† OrthonormalBasis.repr_apply_apply, mul_conj, â† ofReal_pow, â† ofReal_mul,
    ofReal_re]
  exact mul_nonneg (Real.sqrt_nonneg _) (sq_nonneg _)

/-- `T` is positive if and only if `T` is symmetric
(which is automatic from the definition of positivity)
and has a non-negative spectrum -/
theorem isPositive_iff_isSymmetric_and_nonneg_spectrum :
    T.IsPositive â†” T.IsSymmetric âˆ§ (spectrum ğ•œ T).IsNonneg := by
  classical
  refine' âŸ¨fun h => âŸ¨h.1, fun Î¼ hÎ¼ => IsPositive.nonneg_spectrum T h Î¼ hÎ¼âŸ©, fun h => âŸ¨h.1, _âŸ©âŸ©
  intro x
  rw [â† sqrt_sq_eq_self_of_isSymmetric_and_nonneg_spectrum T h.1 h.2, pow_two, mul_apply, â†
    adjoint_inner_left,
    isSelfAdjoint_iff'.mp
      ((isSymmetric_iff_isSelfAdjoint _).mp (IsSymmetric.sqrtIsPositive T h.1).1)]
  exact inner_self_nonneg

/-- `T` is positive if and only if there exists a
linear map `S` such that `T = S.adjoint * S` -/
theorem isPositive_iff_exists_adjoint_hMul_self :
    T.IsPositive â†” âˆƒ S : E â†’â‚—[ğ•œ] E, T = adjoint S * S := by
  classical
  constructor
  Â· rw [isPositive_iff_isSymmetric_and_nonneg_spectrum T]
    rintro âŸ¨hT, hT1âŸ©
    use T.sqrt hT
    rw [isSelfAdjoint_iff'.mp
        ((isSymmetric_iff_isSelfAdjoint _).mp (IsSymmetric.sqrtIsPositive T hT).1),
      â† pow_two]
    exact (sqrt_sq_eq_self_of_isSymmetric_and_nonneg_spectrum T hT hT1).symm
  Â· intro h
    rcases h with âŸ¨S, rflâŸ©
    refine' âŸ¨isSymmetric_adjoint_mul_self S, _âŸ©
    intro x
    simp_rw [mul_apply, adjoint_inner_right]
    exact inner_self_nonneg

section Complex

/-- for spaces `V` over `â„‚`, it suffices to define positivity with
`0 â‰¤ âŸªv, T vâŸ«_â„‚` for all `v âˆˆ V` -/
theorem complex_isPositive {V : Type _} [NormedAddCommGroup V] [InnerProductSpace â„‚ V]
    (T : V â†’â‚—[â„‚] V) : T.IsPositive â†” âˆ€ v : V, â†‘(re âŸªv, T vâŸ«_â„‚) = âŸªv, T vâŸ«_â„‚ âˆ§ 0 â‰¤ re âŸªv, T vâŸ«_â„‚ :=
  by
  simp_rw [IsPositive, isSymmetric_iff_inner_map_self_real, inner_conj_symm,
    RCLike.re_to_complex, â† Complex.conj_eq_iff_re, inner_conj_symm,
    â† forall_and, eq_comm]

end Complex

theorem IsPositive.conjAdjoint [FiniteDimensional ğ•œ F] (T : E â†’â‚—[ğ•œ] E) (S : E â†’â‚—[ğ•œ] F)
    (h : T.IsPositive) : (S.comp (T.comp (adjoint S))).IsPositive :=
  by
  constructor
  intro u v
  simp_rw [comp_apply, â† adjoint_inner_left _ (T _), â† adjoint_inner_right _ (T _) _]
  exact h.1 _ _
  intro v
  simp_rw [comp_apply, â† adjoint_inner_left _ (T _)]
  exact h.2 _

theorem IsPositive.adjointConj [FiniteDimensional ğ•œ F] (T : E â†’â‚—[ğ•œ] E) (S : F â†’â‚—[ğ•œ] E)
    (h : T.IsPositive) : (S.adjoint.comp (T.comp S)).IsPositive :=
  by
  constructor
  intro u v
  simp_rw [comp_apply, adjoint_inner_left, adjoint_inner_right]
  exact h.1 _ _
  intro v
  simp_rw [comp_apply, adjoint_inner_right]
  exact h.2 _

local notation "âˆšTâ‹†" T => LinearMap.sqrt ((LinearMap.adjoint T) âˆ˜â‚— T) (isSymmetric_adjoint_mul_self T)

/-- we have `(T.adjoint.comp T).sqrt` is positive, given any linear map `T` -/
theorem sqrtAdjointSelfIsPositive (T : E â†’â‚—[ğ•œ] E) : (âˆšTâ‹†T).IsPositive :=
  IsSymmetric.sqrtIsPositive _ (isSymmetric_adjoint_mul_self T)

/-- given any linear map `T` and `x âˆˆ E` we have
`â€–(T.adjoint.comp T).sqrt xâ€– = â€–T xâ€–` -/
theorem norm_of_sqrt_adjoint_mul_self_eq (T : E â†’â‚—[ğ•œ] E) (x : E) :
    â€–(âˆšTâ‹†T) xâ€– = â€–T xâ€– :=
  by
  simp_rw [â† sq_eq_sq (norm_nonneg _) (norm_nonneg _), â† @inner_self_eq_norm_sq ğ•œ, â†
    adjoint_inner_left,
    isSelfAdjoint_iff'.mp
      ((isSymmetric_iff_isSelfAdjoint _).mp (sqrtAdjointSelfIsPositive T).1),
    â† mul_eq_comp, â† mul_apply, â† pow_two, mul_eq_comp]
  congr
  apply sqrt_sq_eq_self_of_isSymmetric_and_nonneg_spectrum
  apply IsPositive.nonneg_spectrum _ âŸ¨isSymmetric_adjoint_mul_self T, _âŸ©
  intro x
  simp_rw [mul_apply, adjoint_inner_right]
  exact inner_self_nonneg

theorem invertible_iff_inner_map_self_pos
    (hT : T.IsPositive) : Function.Bijective T â†” âˆ€ v : E, v â‰  0 â†’ 0 < re âŸªT v, vâŸ« :=
  by
  constructor
  Â· intro h v hv
    cases' (isPositive_iff_exists_adjoint_hMul_self T).mp hT with S hS
    rw [hS, mul_apply, adjoint_inner_left, inner_self_eq_norm_sq]
    suffices S v â‰  0 by
      rw [â† norm_ne_zero_iff] at this
      exact sq_pos_iff.mpr this
    by_contra!
    rw [ext_iff] at hS
    specialize hS v
    rw [mul_apply, this, map_zero] at hS
    apply hv
    apply_fun T
    rw [map_zero]
    exact hS
    exact h.1
  Â· intro h
    by_contra!
    rw [Function.Bijective, â† injective_iff_surjective,
      and_self_iff, injective_iff_map_eq_zero] at this
    push_neg at this
    cases' this with a ha
    specialize h a ha.2
    rw [ha.1, inner_zero_left, zero_re', lt_self_iff_false] at h
    exact h

theorem invertiblePos (T : E â†’â‚—[ğ•œ] E) [hTi : Invertible T]
    (hT : T.IsPositive) : IsPositive (â…Ÿ T) :=
  by
  have : Function.Bijective T :=
    by
    refine' (Module.End_isUnit_iff T).mp _
    exact isUnit_of_invertible T
  have t1 := this
  rw [invertible_iff_inner_map_self_pos T hT] at this
  constructor
  Â· intro u v
    rw [â† adjoint_inner_left]
    revert v
    have ugh := ((isSymmetric_iff_isSelfAdjoint T).mp hT.1).star_eq
    -- have hmm : Invertible (adjoint T) := by rw [ugh]; exact hTi
    have t : star (â…Ÿ T) = â…Ÿ (star T) := star_invOf _
    rw [â† ext_inner_left_iff ((â…Ÿ T) u) (adjoint (â…Ÿ T) u), â† star_eq_adjoint]
    simp_rw [t, ugh]
  Â· intro x
    by_cases b : â…Ÿ T x = 0
    Â· rw [b, inner_zero_right, map_zero]
    Â· specialize this _ b
      rw [â† mul_apply, mul_invOf_self, one_apply] at this
      exact le_of_lt this

theorem IsSymmetric.rePow_eq_rankOne {ğ•œ E : Type _} [RCLike ğ•œ] [NormedAddCommGroup E]
    [InnerProductSpace ğ•œ E] [FiniteDimensional ğ•œ E] {T : E â†’â‚—[ğ•œ] E} (hT : T.IsSymmetric) (r : â„) :
    LinearMap.rePow T hT r =
      âˆ‘ i,
        ((hT.eigenvalues rfl i ^ r : â„) : ğ•œ) â€¢
          (rankOne (hT.eigenvectorBasis rfl i) (hT.eigenvectorBasis rfl i) : E â†’L[ğ•œ] E) :=
  by
  simp_rw [LinearMap.ext_iff, LinearMap.rePow_apply,
    ContinuousLinearMap.coe_sum, ContinuousLinearMap.coe_smul,
    LinearMap.sum_apply, LinearMap.smul_apply,
    ContinuousLinearMap.coe_coe]
  intros
  rfl

theorem IsSymmetric.invertible (hT : T.IsSymmetric) [Invertible T] : (â…Ÿ T).IsSymmetric :=
  by
  rw [LinearMap.isSymmetric_iff_isSelfAdjoint, isSelfAdjoint_iff] at hT âŠ¢
  simp_rw [star_invOf]
  simp only [hT, invOf_inj]

theorem isPositive_and_invertible_pos_eigenvalues (hT : T.IsPositive) [Invertible T]
    (i : Fin (FiniteDimensional.finrank ğ•œ E)) : 0 < hT.1.eigenvalues rfl i :=
  by
  -- have := linear_map.invertible_pos T hn hT,
  -- have fs : function.bijective â‡‘(â…Ÿ T),
  have fs : Function.Bijective â‡‘T :=
    by
    rw [Function.bijective_iff_has_inverse]
    useâ‡‘(â…Ÿ T)
    simp_rw [Function.RightInverse, Function.LeftInverse, â† LinearMap.mul_apply, invOf_mul_self,
      mul_invOf_self, LinearMap.one_apply, and_self_iff, forall_const]
  obtain âŸ¨v, hv, ghâŸ© :=
    Module.End.has_eigenvector_iff_hasEigenvalue.mpr
      (@LinearMap.IsSymmetric.hasEigenvalue_eigenvalues ğ•œ _ E _ _ T hT.1 _ _ rfl i)
  have ugh := (LinearMap.invertible_iff_inner_map_self_pos T hT).mp fs v gh
  rw [hv, inner_smul_real_left, RCLike.smul_re, inner_self_eq_norm_sq, mul_pos_iff] at ugh
  simp_rw [not_lt_of_le (sq_nonneg _), and_false_iff, or_false_iff] at ugh
  exact ugh.1

noncomputable def IsPositive.rePowIsInvertible (hT : T.IsPositive) [Invertible T]
    (r : â„) : Invertible (T.rePow hT.1 r) := by
  apply Invertible.mk (T.rePow hT.1 (-r)) <;> ext1 <;>
      simp_rw [LinearMap.mul_apply, LinearMap.rePow_apply, inner_sum, inner_smul_right,
        orthonormal_iff_ite.mp (hT.1.eigenvectorBasis rfl).orthonormal, mul_boole, mul_ite,
        MulZeroClass.mul_zero, Finset.sum_ite_eq, Finset.mem_univ, if_true, smul_smul, â† mul_assoc,
        â† RCLike.ofReal_mul, â†
        Real.rpow_add (LinearMap.isPositive_and_invertible_pos_eigenvalues _ hT _),
        LinearMap.one_apply] <;>
    simp only [add_neg_self, neg_add_self, Real.rpow_zero, RCLike.ofReal_one, one_mul, â†
      OrthonormalBasis.repr_apply_apply, OrthonormalBasis.sum_repr]

theorem IsPositive.sum {ğ•œ E : Type _} [RCLike ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
    {n : â„•} {T : Fin n â†’ E â†’â‚—[ğ•œ] E} (hT : âˆ€ i, (T i).IsPositive) : (âˆ‘ i, T i).IsPositive :=
  by
  induction' n with d hd
  Â· simp only [Finset.univ_eq_empty, Finset.sum_empty, LinearMap.isPositiveZero]
  Â· simp_rw [Fin.sum_univ_castSucc]
    apply LinearMap.IsPositive.add
    apply hd
    Â· intro i
      exact hT _
    Â· exact hT _

theorem IsPositive.smulNonneg {ğ•œ E : Type _} [RCLike ğ•œ] [NormedAddCommGroup E]
    [InnerProductSpace ğ•œ E] {T : E â†’â‚—[ğ•œ] E} (hT : T.IsPositive) {r : â„} (hr : 0 â‰¤ r) :
    ((r : ğ•œ) â€¢ T).IsPositive := by
  simp_rw [LinearMap.IsPositive, LinearMap.IsSymmetric, LinearMap.smul_apply, inner_smul_left,
    inner_smul_right, RCLike.conj_ofReal, RCLike.re_ofReal_mul, hT.1 _ _,
    forallâ‚‚_true_iff, true_and_iff, mul_nonneg hr (hT.2 _), forall_true_iff]
theorem IsPositive.smulNNReal {ğ•œ E : Type _} [RCLike ğ•œ] [NormedAddCommGroup E]
    [InnerProductSpace ğ•œ E] {T : E â†’â‚—[ğ•œ] E} (hT : T.IsPositive) (r : NNReal) :
    (((r : â„) : ğ•œ) â€¢ T).IsPositive :=
hT.smulNonneg r.2

end FiniteDimensional

end LinearMap

namespace ContinuousLinearMap

open ContinuousLinearMap

variable [CompleteSpace E] [CompleteSpace F]

theorem IsPositive.toLinearMap (T : E â†’L[ğ•œ] E) : T.toLinearMap.IsPositive â†” T.IsPositive := by
  simp_rw [LinearMap.IsPositive, ContinuousLinearMap.coe_coe, IsPositive,
    isSelfAdjoint_iff_isSymmetric, reApplyInnerSelf_apply T, inner_re_symm]

end ContinuousLinearMap

theorem rankOne.isPositive {ğ•œ E : Type _} [RCLike ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
    [CompleteSpace E] (x : E) : (rankOne x x : _ â†’L[ğ•œ] _).IsPositive :=
  by
  refine' âŸ¨rankOne.isSelfAdjoint _, _âŸ©
  intro y
  rw [ContinuousLinearMap.reApplyInnerSelf_apply, rankOne_apply, inner_smul_left, RCLike.conj_mul, â† RCLike.ofReal_pow,
    RCLike.ofReal_re]
  exact sq_nonneg _

theorem LinearMap.IsPositive.nonneg_eigenvalue {E : Type _} [NormedAddCommGroup E]
    [InnerProductSpace ğ•œ E] [FiniteDimensional ğ•œ E] {T : E â†’â‚—[ğ•œ] E} (hT : T.IsPositive) {Î± : â„}
    (hÎ± : Module.End.HasEigenvalue T Î±) : 0 â‰¤ Î± :=
  by
  have this := LinearMap.IsPositive.nonneg_spectrum T hT Î±
    (Module.End.hasEigenvalue_iff_mem_spectrum.mp hÎ±)
  rw [zero_le_real] at this
  exact this

open scoped BigOperators

theorem LinearMap.isPositive_iff_eq_sum_rankOne [FiniteDimensional ğ•œ E]
    (T : E â†’â‚—[ğ•œ] E) :
    T.IsPositive â†”
      âˆƒ (m : â„•) (u : Fin m â†’ E), T = âˆ‘ i : Fin m, ((rankOne (u i) (u i) : E â†’L[ğ•œ] E) : E â†’â‚—[ğ•œ] E) :=
  by
  constructor
  Â· intro hT
    let a : Fin (FiniteDimensional.finrank ğ•œ E) â†’ E := fun i =>
      (Real.sqrt (hT.1.eigenvalues rfl i) : ğ•œ) â€¢ hT.1.eigenvectorBasis rfl i
    refine' âŸ¨FiniteDimensional.finrank ğ•œ E, a, _âŸ©
    intros
    ext1
    simp_rw [LinearMap.sum_apply, ContinuousLinearMap.coe_coe, rankOne_apply, a, inner_smul_left,
      smul_smul, mul_assoc, RCLike.conj_ofReal, mul_comm (âŸª_, _âŸ«_ğ•œ),
      â† mul_assoc, â† RCLike.ofReal_mul, â†
      Real.sqrt_mul (hT.nonneg_eigenvalue (hT.1.hasEigenvalue_eigenvalues rfl _)),
      Real.sqrt_mul_self (hT.nonneg_eigenvalue (hT.1.hasEigenvalue_eigenvalues rfl _)),
      mul_comm _ (inner _ _), â† smul_eq_mul, smul_assoc, â† hT.1.apply_eigenvectorBasis, â†
      LinearMap.map_smul, â† map_sum, â† OrthonormalBasis.repr_apply_apply, OrthonormalBasis.sum_repr]
  Â· rintro âŸ¨m, u, huâŸ©
    simp_rw [LinearMap.IsPositive, LinearMap.IsSymmetric, hu, LinearMap.sum_apply,
      ContinuousLinearMap.coe_coe, rankOne_apply, inner_sum, sum_inner, inner_smul_left,
      inner_smul_right, inner_conj_symm, mul_comm, forallâ‚‚_true_iff, true_and_iff,
      map_sum]
    intros
    apply Finset.sum_nonneg'
    intros
    simp_rw [â† inner_conj_symm _ (u _), RCLike.conj_mul, â† RCLike.ofReal_pow,
      RCLike.ofReal_re, sq_nonneg]

theorem LinearMap.IsSymmetric.rePowIsPositiveOfIsPositive {ğ•œ E : Type _} [RCLike ğ•œ]
    [NormedAddCommGroup E] [InnerProductSpace ğ•œ E] [FiniteDimensional ğ•œ E]
    {T : E â†’â‚—[ğ•œ] E} (hT : T.IsPositive) (r : â„) :
    (T.rePow hT.1 r).IsPositive :=
  by
  haveI := FiniteDimensional.complete ğ•œ E
  simp_rw [LinearMap.IsSymmetric.rePow_eq_rankOne, ContinuousLinearMap.coe_sum]
  apply LinearMap.IsPositive.sum
  intro i
  apply LinearMap.IsPositive.smulNonneg
  Â· rw [ContinuousLinearMap.IsPositive.toLinearMap]
    exact rankOne.isPositive _
  Â· apply Real.rpow_nonneg
    exact hT.nonneg_eigenvalue (hT.1.hasEigenvalue_eigenvalues rfl _)
