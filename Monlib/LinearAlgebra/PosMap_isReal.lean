import Monlib.LinearAlgebra.InnerAut
import Monlib.LinearAlgebra.Matrix.IncludeBlock
import Monlib.LinearAlgebra.InvariantSubmodule
import Monlib.LinearAlgebra.Ips.MinimalProj
import Monlib.LinearAlgebra.Nacgor
import Mathlib.Analysis.InnerProductSpace.StarOrder
import Monlib.LinearAlgebra.OfNorm
import Monlib.LinearAlgebra.Matrix.StarOrderedRing
import Monlib.LinearAlgebra.Matrix.PosDefRpow
import Monlib.LinearAlgebra.ToMatrixOfEquiv
import Mathlib.Analysis.InnerProductSpace.Positive
import Mathlib.Analysis.CStarAlgebra.Matrix

variable {A : Type _} [Ring A] [StarRing A] [Algebra â„‚ A] [StarModule â„‚ A] [PartialOrder A]
  [_root_.StarOrderedRing A]

/-- we say a map $f \colon M_1 \to M_2$ is a positive map
  if for all positive $x \in M_1$, we also get $f(x)$ is positive -/
def LinearMap.IsPosMap
  {Mâ‚ Mâ‚‚ : Type*} [Zero Mâ‚] [Zero Mâ‚‚] [PartialOrder Mâ‚] [PartialOrder Mâ‚‚]
  {F : Type*} [FunLike F Mâ‚ Mâ‚‚] (f : F) : Prop :=
âˆ€ â¦ƒx : Mâ‚â¦„, 0 â‰¤ x â†’ 0 â‰¤ f x

noncomputable abbrev selfAdjointDecomposition_left
  {B : Type*} [Star B] [Add B] [SMul â„‚ B] (a : B) :=
(1 / 2 : â„‚) â€¢ (a + star a)
local notation "aL" => selfAdjointDecomposition_left

lemma selfAdjointDecomposition_left_one
  {B : Type*} [AddCommMonoid B] [MulOneClass B] [StarMul B] [Module â„‚ B] :
  aL (1 : B) = 1 :=
by
  rw [selfAdjointDecomposition_left, star_one, â† two_smul â„‚, smul_smul]
  norm_num

noncomputable abbrev selfAdjointDecomposition_right
  {B : Type*} [Star B] [Sub B] [SMul â„‚ B] (a : B) :=
(RCLike.I : â„‚) â€¢ ((1 / 2 : â„‚) â€¢ (star a - a))
local notation "aR" => selfAdjointDecomposition_right

lemma selfAdjointDecomposition_right_one
  {B : Type*} [AddCommGroup B] [MulOneClass B] [StarMul B] [SMulZeroClass â„‚ B] :
    aR (1 : B) = 0 :=
by
  simp_rw [selfAdjointDecomposition_right, star_one, sub_self, smul_zero]

lemma selfAdjointDecomposition_right_eq_zero_iff
  {B : Type*} [Star B] [AddGroup B]
  [SMulWithZero â„‚ B] [NoZeroSMulDivisors â„‚ B] (a : B) :
    aR a = 0 â†” IsSelfAdjoint a :=
by
  simp [selfAdjointDecomposition_right, smul_eq_zero, sub_eq_zero]
  rfl

theorem selfAdjointDecomposition_left_isSelfAdjoint
  {B : Type*} [AddCommMonoid B] [StarAddMonoid B] [SMul â„‚ B] [StarModule â„‚ B] (a : B) :
    IsSelfAdjoint (aL a) :=
by simp [selfAdjointDecomposition_left, isSelfAdjoint_iff, star_smul, add_comm]

theorem selfAdjointDecomposition_right_isSelfAdjoint
  {B : Type*} [AddCommGroup B] [StarAddMonoid B]
  [Module â„‚ B] [StarModule â„‚ B]
  (a : B) :
    IsSelfAdjoint (aR a) :=
by
  simp [selfAdjointDecomposition_right, isSelfAdjoint_iff, star_smul, smul_smul]
  rw [â† neg_sub, â† neg_smul, neg_smul_neg]

theorem selfAdjointDecomposition
  {B : Type*} [AddCommGroup B] [StarAddMonoid B]
  [Module â„‚ B] [StarModule â„‚ B] (a : B) :
  a = aL a + (RCLike.I : â„‚) â€¢ (aR a) :=
by
  simp_rw [selfAdjointDecomposition_left, selfAdjointDecomposition_right,
    smul_smul, â† mul_assoc, RCLike.I, Complex.I_mul_I, smul_add, smul_sub,
    neg_mul, one_mul, neg_smul, neg_sub_neg]
  simp only [one_div, add_add_sub_cancel]
  simp_rw [â† two_smul â„‚, smul_smul]
  simp only [ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, mul_inv_cancelâ‚€, one_smul]

noncomputable
def ContinuousLinearMap.toLinearMapAlgEquiv
  {ğ•œ B : Type*} [RCLike ğ•œ] [NormedAddCommGroup B]
  [InnerProductSpace ğ•œ B] [FiniteDimensional ğ•œ B] :
  (B â†’L[ğ•œ] B) â‰ƒâ‚[ğ•œ] (B â†’â‚—[ğ•œ] B) :=
AlgEquiv.ofLinearEquiv LinearMap.toContinuousLinearMap.symm rfl (Î» _ _ => rfl)

lemma ContinuousLinearMap.toLinearMapAlgEquiv_apply
  {ğ•œ B : Type*} [RCLike ğ•œ] [NormedAddCommGroup B]
  [InnerProductSpace ğ•œ B] [FiniteDimensional ğ•œ B]
  (f : B â†’L[ğ•œ] B) :
  ContinuousLinearMap.toLinearMapAlgEquiv f = f.toLinearMap :=
rfl

lemma ContinuousLinearMap.toLinearMapAlgEquiv_symm_apply
  {ğ•œ B : Type*} [RCLike ğ•œ] [NormedAddCommGroup B]
  [InnerProductSpace ğ•œ B] [FiniteDimensional ğ•œ B]
  (f : B â†’â‚—[ğ•œ] B) :
  ContinuousLinearMap.toLinearMapAlgEquiv.symm f = LinearMap.toContinuousLinearMap f :=
rfl

theorem ContinuousLinearMap.spectrum_coe {ğ•œ B : Type*} [RCLike ğ•œ] [NormedAddCommGroup B]
  [InnerProductSpace ğ•œ B] [FiniteDimensional ğ•œ B] (T : B â†’L[ğ•œ] B) :
  spectrum ğ•œ T.toLinearMap = spectrum ğ•œ T :=
by rw [â† ContinuousLinearMap.toLinearMapAlgEquiv_apply, AlgEquiv.spectrum_eq]

variable {B : Type*} [NormedAddCommGroup B] [InnerProductSpace â„‚ B]
  [FiniteDimensional â„‚ B]

open scoped MatrixOrder ComplexOrder FiniteDimensional
-- set_option synthInstance.checkSynthOrder false in
-- attribute [instance] FiniteDimensional.complete
theorem ContinuousLinearMap.nonneg_iff_isSelfAdjoint_and_nonneg_spectrum
  (T : B â†’L[â„‚] B) :
  0 â‰¤ T â†” IsSelfAdjoint T âˆ§ spectrum â„‚ T âŠ† {a : â„‚ | 0 â‰¤ a} :=
by
  rw [nonneg_iff_isPositive, ContinuousLinearMap.IsPositive.toLinearMap',
    LinearMap.isPositive'_iff_isSymmetric_and_nonneg_spectrum,
    isSelfAdjoint_iff_isSymmetric, spectrum_coe]

theorem ContinuousLinearMap.nonneg_iff_exists
  (T : B â†’L[â„‚] B) :
  0 â‰¤ T â†” âˆƒ f, T = star f * f :=
by rw [nonneg_iff_isPositive]; exact isPositive_iff_exists_adjoint_hMul_self _

-- def ContinuousLinearMap.StarOrderedRing :
--   _root_.StarOrderedRing (B â†’L[â„‚] B) :=
-- StarOrderedRing.of_nonneg_iff'
--   (Î» hxy z => by simp_rw [le_def, add_sub_add_left_eq_sub]; exact hxy)
--   (Î» x => by
--     rw [nonneg_iff_isPositive]
--     exact ContinuousLinearMap.isPositive_iff_exists_adjoint_hMul_self _)
-- attribute [local instance] ContinuousLinearMap.StarOrderedRing

lemma orthogonalProjection_ker_comp_eq_of_comp_eq_zero {T S : B â†’L[â„‚] B} (h : T * S = 0) :
  orthogonalProjection' (LinearMap.ker T) * S = S :=
by
  have : LinearMap.range S â‰¤ LinearMap.ker T := by
    intro x âŸ¨y, hyâŸ©
    rw [â† hy, LinearMap.mem_ker, â† ContinuousLinearMap.mul_apply, h,
      ContinuousLinearMap.zero_apply]
  rw [â† orthogonalProjection.range (LinearMap.ker T)] at this
  rw [â† ContinuousLinearMap.coe_inj, ContinuousLinearMap.mul_def,
    ContinuousLinearMap.coe_comp, IsIdempotentElem.comp_idempotent_iff]
  . simp only [Submodule.map_top, ContinuousLinearMap.range_toLinearMap]
    exact this
  . simp only [â† ContinuousLinearMap.IsIdempotentElem.toLinearMap]
    exact orthogonalProjection.isIdempotentElem _

variable {n : Type*} [Fintype n] [DecidableEq n]

/-- matrix of orthogonalProjection -/
noncomputable def Matrix.orthogonalProjection
  (U : Submodule â„‚ (EuclideanSpace â„‚ n)) :
  Matrix n n â„‚ :=
(Matrix.toEuclideanCLM (ğ•œ := â„‚)).symm (orthogonalProjection' U)

noncomputable def PiMat.orthogonalProjection
  {k : Type*} {n : k â†’ Type*}
  [Î  i, Fintype (n i)] [Î  i, DecidableEq (n i)]
  (U : Î  i, Submodule â„‚ (EuclideanSpace â„‚ (n i))) :
    PiMat â„‚ k n :=
Î» i => Matrix.orthogonalProjection (U i)

lemma Matrix.toEuclideanLin_symm {ğ•œ n : Type*} [RCLike ğ•œ] [Fintype n] [DecidableEq n]
  (x : EuclideanSpace ğ•œ n â†’â‚—[ğ•œ] EuclideanSpace ğ•œ n) :
  (Matrix.toEuclideanLin.symm x) = LinearMap.toMatrix' x :=
rfl
lemma EuclideanSpace.trace_eq_matrix_trace' {ğ•œ n : Type*}  [RCLike ğ•œ] [Fintype n] [DecidableEq n] (f : (EuclideanSpace ğ•œ n) â†’â‚—[ğ•œ] (EuclideanSpace ğ•œ n)) :
  LinearMap.trace ğ•œ _ f = Matrix.trace (Matrix.toEuclideanLin.symm f) :=
by
  rw [Matrix.toEuclideanLin_symm, â† LinearMap.toMatrix_eq_toMatrix',
    â† LinearMap.trace_eq_matrix_trace]
  rfl

theorem Matrix.coe_toEuclideanCLM_symm_eq_toEuclideanLin_symm {ğ•œ n : Type*}
  [RCLike ğ•œ] [Fintype n] [DecidableEq n]
  (A : EuclideanSpace ğ•œ n â†’L[ğ•œ] EuclideanSpace ğ•œ n) :
  (toEuclideanCLM (ğ•œ := ğ•œ)).symm A = toEuclideanLin.symm A :=
rfl

theorem Matrix.orthogonalProjection_trace {U : Submodule â„‚ (EuclideanSpace â„‚ n)} :
  (Matrix.orthogonalProjection U).trace = Module.finrank â„‚ U :=
by
  rw [orthogonalProjection, Matrix.coe_toEuclideanCLM_symm_eq_toEuclideanLin_symm, â† EuclideanSpace.trace_eq_matrix_trace']
  exact _root_.orthogonalProjection_trace _

theorem PiMat.orthogonalProjection_trace {k : Type*} {n : k â†’ Type*} [Fintype k] [DecidableEq k]
  [Î  i, Fintype (n i)] [Î  i, DecidableEq (n i)]
  (U : Î  i, Submodule â„‚ (EuclideanSpace â„‚ (n i))) :
  (Matrix.blockDiagonal' (PiMat.orthogonalProjection U)).trace
    = âˆ‘ i, Module.finrank â„‚ (U i) :=
by
  simp_rw [Matrix.trace_blockDiagonal', PiMat.orthogonalProjection,
    Matrix.orthogonalProjection_trace, Nat.cast_sum]

lemma Matrix.isIdempotentElem_toEuclideanCLM {n : Type*} [Fintype n] [DecidableEq n]
  (x : Matrix n n â„‚) :
  IsIdempotentElem x â†” IsIdempotentElem (toEuclideanCLM (ğ•œ := â„‚) x) :=
by
  simp_rw [IsIdempotentElem, â† _root_.map_mul]
  exact Iff.symm (EmbeddingLike.apply_eq_iff_eq toEuclideanCLM)

lemma Matrix.CLM_apply_orthogonalProjection {U : Submodule â„‚ (EuclideanSpace â„‚ n)} :
  Matrix.toEuclideanCLM (ğ•œ := â„‚) (Matrix.orthogonalProjection U)
    = orthogonalProjection' U :=
by
  ext1
  simp [orthogonalProjection', orthogonalProjection]

lemma Matrix.orthogonalProjection_ortho_eq {U : Submodule â„‚ (EuclideanSpace â„‚ n)} :
  Matrix.orthogonalProjection Uá—® = 1 - Matrix.orthogonalProjection U :=
by
  apply_fun Matrix.toEuclideanCLM (ğ•œ := â„‚)
  simp only [_root_.map_mul, map_sub, _root_.map_one]
  simp only [Matrix.CLM_apply_orthogonalProjection]
  exact orthogonalProjection.orthogonal_complement_eq U

lemma Matrix.orthogonalProjection_isPosSemidef {U : Submodule â„‚ (EuclideanSpace â„‚ n)} :
  (Matrix.orthogonalProjection U).PosSemidef :=
by
  rw [posSemidef_eq_linearMap_positive', â† coe_toEuclideanCLM_eq_toEuclideanLin,
    Matrix.CLM_apply_orthogonalProjection,
    â† ContinuousLinearMap.IsPositive.toLinearMap',
    â† ContinuousLinearMap.nonneg_iff_isPositive]
  exact orthogonalProjection.is_positive

lemma Matrix.IsHermitian.orthogonalProjection_ker_apply_self {x : Matrix n n â„‚}
  (hx : x.IsHermitian) :
  Matrix.orthogonalProjection (LinearMap.ker (toEuclideanCLM (ğ•œ := â„‚) x)) * x = 0 :=
by
  apply_fun Matrix.toEuclideanCLM (ğ•œ := â„‚)
  simp only [_root_.map_mul, map_zero]
  simp only [Matrix.CLM_apply_orthogonalProjection]
  ext1
  simp only [ContinuousLinearMap.mul_apply, ContinuousLinearMap.zero_apply]
  simp only [orthogonalProjection'_eq, ContinuousLinearMap.coe_comp', Submodule.coe_subtypeL',
    Submodule.coe_subtype, Function.comp_apply, ZeroMemClass.coe_eq_zero,
    Submodule.orthogonalProjection_eq_zero_iff]
  rw [ContinuousLinearMap.ker_eq_ortho_adjoint_range, Submodule.orthogonal_orthogonal,
    â† ContinuousLinearMap.star_eq_adjoint]
  simp only [â† map_star, star_eq_conjTranspose, hx.eq]
  exact LinearMap.mem_range_self _ _

private lemma auxaux_2 {T S : Matrix n n â„‚} (h : T * S = 0) :
  Matrix.orthogonalProjection (LinearMap.ker (Matrix.toEuclideanCLM (ğ•œ := â„‚) T)) * S = S :=
by
  apply_fun Matrix.toEuclideanCLM (ğ•œ := â„‚) at h âŠ¢
  simp only [_root_.map_mul, map_zero] at h âŠ¢
  simp only [Matrix.CLM_apply_orthogonalProjection]
  exact orthogonalProjection_ker_comp_eq_of_comp_eq_zero h

theorem Matrix.nonneg_def {x : Matrix n n â„‚} :
  0 â‰¤ x â†” x.PosSemidef :=
by rw [le_iff, sub_zero]

/-- the positive square root of the square of a Hermitian matrix `x`,
  in other words, `âˆš(x ^ 2)` -/
noncomputable def Matrix.IsHermitian.sqSqrt {x : Matrix n n â„‚}
  (hx : x.IsHermitian) :
  Matrix n n â„‚ :=
innerAut (hx.eigenvectorUnitary)
  (Matrix.diagonal (RCLike.ofReal âˆ˜ (Î» i => âˆš ((hx.eigenvalues i) ^ 2 : â„))))

lemma Matrix.IsHermitian.sqSqrt_eq {x : Matrix n n â„‚} (hx : x.IsHermitian) :
  hx.sqSqrt = innerAut (hx.eigenvectorUnitary)
    (Matrix.diagonal (RCLike.ofReal âˆ˜ (|hx.eigenvalues|))) :=
by simp only [sqSqrt, Real.sqrt_sq_eq_abs]; rfl

lemma Matrix.IsHermitian.sqSqrt_isPosSemidef {x : Matrix n n â„‚} (hx : x.IsHermitian) :
  hx.sqSqrt.PosSemidef :=
by
  rw [sqSqrt_eq]
  apply (innerAut_posSemidef_iff _).mpr
  apply (diagonal_posSemidef_iff _).mpr
  intro
  rw [Function.comp_apply, Pi.zero_apply, RCLike.ofReal_nonneg, Pi.abs_apply]
  exact abs_nonneg _

/-- the square of the positive square root of a Hermitian matrix is equal to the square of the matrix -/
lemma Matrix.IsHermitian.sqSqrt_sq {x : Matrix n n â„‚} (hx : x.IsHermitian) :
  hx.sqSqrt ^ 2 = x ^ 2 :=
by
  symm
  nth_rw 1 [hx.spectral_theorem'']
  simp_rw [sqSqrt, innerAut.map_pow, diagonal_pow]
  congr
  simp only [diagonal_eq_diagonal_iff, Pi.pow_apply, Function.comp_apply, â† RCLike.ofReal_pow,
    Real.sq_sqrt (sq_nonneg _), implies_true]

noncomputable def Matrix.IsHermitian.posSemidefDecomposition_left (x : Matrix n n â„‚) [hx : Fact x.IsHermitian] :=
(1 / 2 : â„‚) â€¢ (hx.out.sqSqrt + x)
notation3:80 (name := posL) x:81"â‚Š" =>
  @Matrix.IsHermitian.posSemidefDecomposition_left _ _ _ x _
lemma Matrix.IsHermitian.posSemidefDecomposition_left_isHermitian
  {x : Matrix n n â„‚} [hx : Fact x.IsHermitian] : xâ‚Š.IsHermitian :=
by
  rw [IsHermitian, posSemidefDecomposition_left, conjTranspose_smul,
    conjTranspose_add, hx.out.eq, RCLike.star_def]
  simp only [one_div, map_invâ‚€, map_ofNat]
  congr
  apply (innerAut_isHermitian_iff _ _).mp _
  rw [Matrix.isHermitian_diagonal_iff]
  intro i
  simp only [Function.comp_apply, _root_.IsSelfAdjoint, RCLike.star_def, RCLike.conj_ofReal]

noncomputable def Matrix.IsHermitian.posSemidefDecomposition_right
  (x : Matrix n n â„‚) [hx : Fact x.IsHermitian] :=
(1 / 2 : â„‚) â€¢ (hx.out.sqSqrt - x)
notation3:80 (name := posR) x:81"â‚‹" =>
  Matrix.IsHermitian.posSemidefDecomposition_right x
lemma Matrix.IsHermitian.posSemidefDecomposition_right_isHermitian
  {x : Matrix n n â„‚} [hx : Fact x.IsHermitian] :
  xâ‚‹.IsHermitian :=
by
  rw [IsHermitian, posSemidefDecomposition_right, conjTranspose_smul,
    conjTranspose_sub, hx.out.eq, RCLike.star_def]
  simp only [one_div, map_invâ‚€, map_ofNat]
  congr
  apply (innerAut_isHermitian_iff _ _).mp _
  rw [Matrix.isHermitian_diagonal_iff]
  intro i
  simp only [Function.comp_apply, _root_.IsSelfAdjoint, RCLike.star_def, RCLike.conj_ofReal]

/-- a Hermitian matrix commutes with its positive squared-square-root,
  i.e., `x * âˆš(x^2) = âˆš(x^2) * x`. -/
lemma Matrix.IsHermitian.commute_sqSqrt {x : Matrix n n â„‚} (hx : x.IsHermitian) :
  Commute x hx.sqSqrt :=
by
  rw [Commute, SemiconjBy]
  nth_rw 1 [hx.spectral_theorem'']
  symm
  nth_rw 2 [hx.spectral_theorem'']
  simp only [sqSqrt, â† Matrix.innerAut.map_mul, diagonal_mul_diagonal, mul_comm]

lemma Matrix.IsHermitian.posSemidefDecomposition_left_mul_right (x : Matrix n n â„‚)
  [hx : Fact x.IsHermitian] :
  xâ‚Š * xâ‚‹ = 0 :=
by
  simp_rw [posSemidefDecomposition_left, posSemidefDecomposition_right,
    smul_add, smul_sub, add_mul, mul_sub]
  simp only [one_div, Algebra.mul_smul_comm, Algebra.smul_mul_assoc]
  simp_rw [â† pow_two, hx.out.commute_sqSqrt.eq]
  simp only [sub_add_sub_cancel, sqSqrt_sq, hx.out.eq, â† pow_two]
  simp only [sub_self]
lemma Matrix.IsHermitian.posSemidefDecomposition_right_mul_left (x : Matrix n n â„‚)
  [hx : Fact x.IsHermitian] :
  xâ‚‹ * xâ‚Š = 0 :=
by
  simp_rw [posSemidefDecomposition_left, posSemidefDecomposition_right,
    smul_add, smul_sub, sub_mul, mul_add]
  simp only [one_div, Algebra.mul_smul_comm, Algebra.smul_mul_assoc]
  simp_rw [â† pow_two, hx.out.commute_sqSqrt.eq]
  nth_rw 1 [sqSqrt_sq]
  simp only [sub_add_eq_sub_sub, add_sub_cancel_right, sub_self]

lemma Matrix.IsHermitian.posSemidefDecomposition_eq (x : Matrix n n â„‚) [hx : Fact x.IsHermitian] :
  x = xâ‚Š - xâ‚‹ :=
by
  simp_rw [posSemidefDecomposition_left, posSemidefDecomposition_right,
    â† smul_sub, add_sub_sub_cancel, â† two_smul â„‚, smul_smul]
  norm_num

theorem Matrix.IsHermitian.posSemidefDecomposition_posSemidef_left_right
  {x : Matrix n n â„‚} (hx : Fact x.IsHermitian) :
  xâ‚Š.PosSemidef âˆ§ xâ‚‹.PosSemidef  :=
by
  have h := Î» (a b : Matrix n n â„‚) (ha : 0 â‰¤ a) (hb : 0 â‰¤ b)
    => Matrix.posSemidef_iff_commute ha hb
  simp only [sub_zero, Matrix.nonneg_def] at h
  have hâ‚‚ := auxaux_2 (IsHermitian.posSemidefDecomposition_left_mul_right x)
  have hâ‚ƒ : hx.out.sqSqrt = xâ‚Š + xâ‚‹ :=
  by
    simp_rw [IsHermitian.posSemidefDecomposition_left, IsHermitian.posSemidefDecomposition_right,
      â† smul_add, add_add_sub_cancel, â† two_smul â„‚, smul_smul]
    norm_num
  have hâ‚„ : orthogonalProjection (LinearMap.ker (toEuclideanCLM (ğ•œ := â„‚) (xâ‚Š))) * hx.out.sqSqrt = xâ‚‹ :=
  by
    rw [hâ‚ƒ, mul_add, hâ‚‚, Matrix.IsHermitian.orthogonalProjection_ker_apply_self
      IsHermitian.posSemidefDecomposition_left_isHermitian, zero_add]
  have hâ‚… : x = (1 - (2 : â„‚) â€¢ (orthogonalProjection (LinearMap.ker (toEuclideanCLM (ğ•œ := â„‚) (xâ‚Š)))))
    * hx.out.sqSqrt :=
  by
    rw [sub_mul, smul_mul_assoc, hâ‚„, hâ‚ƒ, one_mul, two_smul, add_sub_add_right_eq_sub]
    exact IsHermitian.posSemidefDecomposition_eq _
  have hâ‚† : xâ‚Š = (orthogonalProjection (LinearMap.ker (toEuclideanCLM (ğ•œ := â„‚) (xâ‚Š)))á—®)
    * hx.out.sqSqrt :=
  by
    nth_rw 1 [IsHermitian.posSemidefDecomposition_left]
    nth_rw 3 [hâ‚…]
    rw [Matrix.orthogonalProjection_ortho_eq]
    simp_rw [sub_mul, one_mul, smul_add, smul_sub, smul_mul_assoc,
      hâ‚„, smul_smul, add_sub, â† smul_add, â† two_smul â„‚, smul_smul]
    norm_num
  have hâ‚„' : xâ‚‹ = hx.out.sqSqrt * (orthogonalProjection (LinearMap.ker (toEuclideanCLM (ğ•œ := â„‚) (xâ‚Š)))) :=
  by rw [â† IsHermitian.posSemidefDecomposition_right_isHermitian, â† hâ‚„,
    conjTranspose_mul, (IsHermitian.sqSqrt_isPosSemidef _).1.eq,
    Matrix.orthogonalProjection_isPosSemidef.1.eq]
  have hâ‚†' : xâ‚Š = hx.out.sqSqrt * (orthogonalProjection (LinearMap.ker (toEuclideanCLM (ğ•œ := â„‚) (xâ‚Š)))á—®) :=
  by
    nth_rw 1 [â† IsHermitian.posSemidefDecomposition_left_isHermitian, hâ‚†]
    rw [conjTranspose_mul, (IsHermitian.sqSqrt_isPosSemidef _).1.eq,
      Matrix.orthogonalProjection_isPosSemidef.1.eq]
  constructor
  . rw [hâ‚†', â† h _ _ (IsHermitian.sqSqrt_isPosSemidef _)
      orthogonalProjection_isPosSemidef, Commute, SemiconjBy, â† hâ‚†, â† hâ‚†']
  . rw [hâ‚„', â† h _ _ (IsHermitian.sqSqrt_isPosSemidef _)
      orthogonalProjection_isPosSemidef, Commute, SemiconjBy, hâ‚„, â† hâ‚„']

open scoped Matrix
theorem Matrix.IsHermitian.posSemidefDecomposition'
  {x : Matrix n n â„‚} (hx : x.IsHermitian) :
  âˆƒ a b, x = aá´´ * a - bá´´ * b :=
by
  let hx := Fact.mk hx
  simp_rw [posSemidefDecomposition_eq]
  obtain âŸ¨Î±, hÎ±âŸ© := posSemidef_iff_eq_conjTranspose_mul_self.mp
    (posSemidefDecomposition_posSemidef_left_right hx).1
  obtain âŸ¨Î², hÎ²âŸ© := posSemidef_iff_eq_conjTranspose_mul_self.mp
    (posSemidefDecomposition_posSemidef_left_right hx).2
  use Î±, Î²
  rw [â† hÎ±, â† hÎ²]

theorem PiMat.IsSelfAdjoint.posSemidefDecomposition {k : Type*} {n : k â†’ Type*}
  [Î  i, Fintype (n i)] [Î  i, DecidableEq (n i)]
  {x : PiMat â„‚ k n} (hx : IsSelfAdjoint x) :
  âˆƒ a b, x = star a * a - star b * b :=
by
  have : âˆ€ i, (x i).IsHermitian := Î» i =>
  by
    rw [IsSelfAdjoint, funext_iff] at hx
    simp_rw [Pi.star_apply, Matrix.star_eq_conjTranspose] at hx
    exact hx i
  have := Î» i => Matrix.IsHermitian.posSemidefDecomposition' (this i)
  let a : PiMat â„‚ k n :=
  Î» i => (this i).choose
  let b : PiMat â„‚ k n :=
  Î» i => (this i).choose_spec.choose
  have hab : âˆ€ i, x i = star (a i) * (a i) - star (b i) * (b i) :=
  Î» i => (this i).choose_spec.choose_spec
  use a, b
  ext1
  simp only [Pi.sub_apply, Pi.mul_apply, Pi.star_apply, hab]

open ContinuousLinearMap in
theorem IsSelfAdjoint.isPositiveDecomposition
  {x : B â†’L[â„‚] B} (hx : IsSelfAdjoint x) :
  âˆƒ a b, x = star a * a - star b * b :=
by
  let e := stdOrthonormalBasis â„‚ B
  have hx' : Matrix.IsHermitian (e.toMatrix x.toLinearMap) :=
  by
    rw [Matrix.IsHermitian, â† Matrix.star_eq_conjTranspose, â† map_star]
    congr
    rw [isSelfAdjoint_iff_isSymmetric, LinearMap.isSymmetric_iff_isSelfAdjoint] at hx
    exact hx
  obtain âŸ¨a, b, habâŸ© := hx'.posSemidefDecomposition'
  let a' : B â†’L[â„‚] B := LinearMap.toContinuousLinearMap (e.toMatrix.symm a)
  let b' : B â†’L[â„‚] B := LinearMap.toContinuousLinearMap (e.toMatrix.symm b)
  use a', b'
  apply_fun e.toMatrix.symm at hab
  simp_rw [StarAlgEquiv.symm_apply_apply, map_sub, map_mul, â† Matrix.star_eq_conjTranspose,
    map_star] at hab
  calc x = LinearMap.toContinuousLinearMap (x.toLinearMap) := rfl
    _ = LinearMap.toContinuousLinearMap (star (e.toMatrix.symm a)) * a' -
      LinearMap.toContinuousLinearMap (star (e.toMatrix.symm b)) * b' := ?_
    _ = star a' * a' - star b' * b' := rfl
  . rw [hab, â† toLinearMapAlgEquiv_symm_apply, map_sub, map_mul]
    rfl

structure isEquivToPiMat (A : Type*) [Add A] [Mul A] [Star A] [SMul â„‚ A] where
  k : Type*
  -- hnâ‚ : Fintype n
  -- hnâ‚‚ : DecidableEq n
  rk : k â†’ Type*
  hrkâ‚ : Î  i, Fintype (rk i)
  hrkâ‚‚ : Î  i, DecidableEq (rk i)
  Ï† : A â‰ƒâ‹†â‚[â„‚] PiMat â„‚ k rk
-- attribute [instance] isEquivToPiMat.hnâ‚
-- attribute [instance] isEquivToPiMat.hnâ‚‚
attribute [instance] isEquivToPiMat.hrkâ‚
attribute [instance] isEquivToPiMat.hrkâ‚‚

noncomputable def Matrix.isEquivToPiMat :
  isEquivToPiMat (Matrix n n â„‚) :=
let f : Matrix n n â„‚ â‰ƒâ‹†â‚[â„‚] PiMat â„‚ Unit (Î» _ => n) :=
{ toFun := Î» x _ => x
  invFun := Î» x => x ()
  left_inv := Î» x => rfl
  right_inv := Î» x => funext (Î» i => rfl)
  map_mul' := Î» x y => rfl
  map_add' := Î» x y => rfl
  map_smul' := Î» x y => rfl
  map_star' := Î» x => rfl }
{ k := Unit
  rk := Î» _ => n
  Ï† := f
  hrkâ‚ := fun i â†¦ by infer_instance
  hrkâ‚‚ := fun i â†¦ by infer_instance }

omit [StarModule â„‚ A] [PartialOrder A] [StarOrderedRing A] in
theorem IsSelfAdjoint.isPositiveDecomposition_of_starAlgEquiv_piMat
  (hÏ† : isEquivToPiMat A)
  {x : A} (hx : _root_.IsSelfAdjoint x) :
  âˆƒ a b, x = star a * a - star b * b :=
by
  have : IsSelfAdjoint (hÏ†.Ï† x) := by
    rw [IsSelfAdjoint, â† map_star, hx]
  obtain âŸ¨Î±, Î², hâŸ© := PiMat.IsSelfAdjoint.posSemidefDecomposition this
  use hÏ†.Ï†.symm Î±, hÏ†.Ï†.symm Î²
  apply_fun hÏ†.Ï†
  simp_rw [h, map_sub, map_mul, map_star, StarAlgEquiv.apply_symm_apply]

/-- if a map preserves positivity, then it is star-preserving -/
theorem Matrix.isReal_of_isPosMap
  {K : Type*}
  [Ring K] [StarRing K] [PartialOrder K] [Algebra â„‚ K] [StarOrderedRing K] [StarModule â„‚ K]
  {Ï† : Matrix n n â„‚ â†’â‚—[â„‚] K} (hÏ† : LinearMap.IsPosMap Ï†) :
  LinearMap.IsReal Ï† :=
by
  intro x
  rw [selfAdjointDecomposition x]
  let L := aL x
  have hL : L = aL x := rfl
  let R := aR x
  have hR : R = aR x := rfl
  rw [â† hL, â† hR]
  simp only [star_add, map_add, star_smul, _root_.map_smul]
  repeat rw [selfAdjointDecomposition_left_isSelfAdjoint _]
  suffices h2 : âˆ€ a (_ : _root_.IsSelfAdjoint a),
      Ï† (star a) = star (Ï† a)
  by
    rw [â† h2 _ (selfAdjointDecomposition_left_isSelfAdjoint _),
      â† h2 _ (selfAdjointDecomposition_right_isSelfAdjoint _),
      selfAdjointDecomposition_left_isSelfAdjoint,
      selfAdjointDecomposition_right_isSelfAdjoint]
  intro x hx
  obtain âŸ¨a, b, rflâŸ© := Matrix.IsHermitian.posSemidefDecomposition' hx
  simp only [star_sub, star_mul, star_star, map_sub, â† star_eq_conjTranspose]
  rw [IsSelfAdjoint.of_nonneg (hÏ† (star_mul_self_nonneg a)),
    IsSelfAdjoint.of_nonneg (hÏ† (star_mul_self_nonneg b))]

theorem StarNonUnitalAlgHom.toLinearMap_apply
  {R A B : Type*} [Semiring R] [NonUnitalNonAssocSemiring A]
  [Module R A] [NonUnitalNonAssocSemiring B] [Module R B]
  [Star A] [Star B]
  (f : A â†’â‹†â‚™â‚[R] B) (x : A) : (f : A â†’â‚—[R] B) x = f x := rfl

theorem LinearMap.isPosMap_iff_star_mul_self_nonneg {A K : Type*}
  [NonUnitalSemiring A] [PartialOrder A] [StarRing A] [StarOrderedRing A]
  [NonUnitalSemiring K] [PartialOrder K] [StarRing K] [StarOrderedRing K]
  (hA : âˆ€ â¦ƒa : Aâ¦„, 0 â‰¤ a â†” âˆƒ b, a = star b * b)
  {F : Type*} [FunLike F A K] {f : F} :
  LinearMap.IsPosMap f â†” âˆ€ a : A, 0 â‰¤ f (star a * a) :=
by
  refine âŸ¨Î» h a => h (star_mul_self_nonneg _), Î» h a => ?_âŸ©
  . rw [hA]
    rintro âŸ¨b, rflâŸ©
    exact h _

theorem LinearMap.isPosMap_iff_comp_starAlgEquiv
  {K A B : Type*}
  [Mul K] [Mul A] [Mul B] [Star K] [Star A] [Star B]
  [Zero A] [Zero B] [Zero K]
  [PartialOrder A] [PartialOrder B] [PartialOrder K]
  (hA : âˆ€ â¦ƒa : Aâ¦„, 0 â‰¤ a â†” âˆƒ b, a = star b * b)
  (hB : âˆ€ â¦ƒa : Bâ¦„, 0 â‰¤ a â†” âˆƒ b, a = star b * b)
  {F S : Type*} [FunLike F A K] {Ï† : F}
  [EquivLike S B A] [MulEquivClass S B A] [StarHomClass S B A]
  (Ïˆ : S) :
  LinearMap.IsPosMap Ï† â†” âˆ€ â¦ƒxâ¦„, 0 â‰¤ x â†’ 0 â‰¤ Ï† (Ïˆ x) :=
by
  simp_rw [IsPosMap, hA, hB]
  simp only [forall_exists_index, forall_eq_apply_imp_iff, map_mul,
    map_star]
  refine âŸ¨Î» h _ => h _, Î» h x => ?_âŸ©
  simpa using h (EquivLike.inv Ïˆ x)

theorem LinearMap.isReal_iff_comp_starEquiv
  {K A B : Type*}
  [Star K] [Star A] [Star B]
  {F S : Type*} [FunLike F A K] [EquivLike S B A] [StarHomClass S B A]
  {Ï† : F} (Ïˆ : S) :
  LinearMap.IsReal Ï† â†” âˆ€ x, Ï† (Ïˆ (star x)) = star (Ï† (Ïˆ x)) :=
by
  simp_rw [map_star]
  refine' âŸ¨Î» h _ => h _, Î» h x => _âŸ©
  simpa using h (EquivLike.inv Ïˆ x)

/-- if a map preserves positivity, then it is star-preserving -/
theorem isReal_of_isPosMap
  {K : Type*}
  [Ring K] [StarRing K] [PartialOrder K] [Algebra â„‚ K] [StarOrderedRing K] [StarModule â„‚ K]
  {Ï† : (B â†’L[â„‚] B) â†’â‚—[â„‚] K} (hÏ† : LinearMap.IsPosMap Ï†) :
  LinearMap.IsReal Ï† :=
by
  intro x
  rw [selfAdjointDecomposition x]
  let L := aL x
  have hL : L = aL x := rfl
  let R := aR x
  have hR : R = aR x := rfl
  rw [â† hL, â† hR]
  simp only [star_add, map_add, star_smul, map_smul]
  repeat rw [selfAdjointDecomposition_left_isSelfAdjoint _]
  suffices h2 : âˆ€ a (_ : IsSelfAdjoint a),
      Ï† (star a) = star (Ï† a)
  by rw [â† h2 _ (selfAdjointDecomposition_left_isSelfAdjoint _),
    â† h2 _ (selfAdjointDecomposition_right_isSelfAdjoint _),
    selfAdjointDecomposition_left_isSelfAdjoint,
    selfAdjointDecomposition_right_isSelfAdjoint]
  intro x hx
  obtain âŸ¨a, b, rflâŸ© := hx.isPositiveDecomposition
  simp only [star_sub, star_mul, star_star, map_sub]
  rw [IsSelfAdjoint.of_nonneg (hÏ† (star_mul_self_nonneg a)),
    IsSelfAdjoint.of_nonneg (hÏ† (star_mul_self_nonneg b))]

theorem isReal_of_isPosMap_of_starAlgEquiv_piMat
  (hÏ† : isEquivToPiMat A)
  {K : Type*}
  [Ring K] [StarRing K] [PartialOrder K] [Algebra â„‚ K] [StarOrderedRing K] [StarModule â„‚ K]
  {f : A â†’â‚—[â„‚] K} (hf : LinearMap.IsPosMap f) :
  LinearMap.IsReal f :=
by
  intro x
  rw [selfAdjointDecomposition x]
  let L := aL x
  have hL : L = aL x := rfl
  let R := aR x
  have hR : R = aR x := rfl
  rw [â† hL, â† hR]
  simp only [star_add, map_add, star_smul, map_smul]
  repeat rw [selfAdjointDecomposition_left_isSelfAdjoint _]
  suffices h2 : âˆ€ a (_ : IsSelfAdjoint a),
      f (star a) = star (f a)
  by rw [â† h2 _ (selfAdjointDecomposition_left_isSelfAdjoint _),
    â† h2 _ (selfAdjointDecomposition_right_isSelfAdjoint _),
    selfAdjointDecomposition_left_isSelfAdjoint,
    selfAdjointDecomposition_right_isSelfAdjoint]
  intro x hx
  obtain âŸ¨a, b, rflâŸ© := hx.isPositiveDecomposition_of_starAlgEquiv_piMat hÏ†
  simp only [star_sub, star_mul, star_star, map_sub]
  rw [IsSelfAdjoint.of_nonneg (hf (star_mul_self_nonneg a)),
    IsSelfAdjoint.of_nonneg (hf (star_mul_self_nonneg b))]

/-- a $^*$-homomorphism from $A$ to $B$ is a positive map -/
theorem starMulHom_isPosMap
  {A K : Type*}
  [Semiring A] [PartialOrder A] [StarRing A] [StarOrderedRing A]
  [Semiring K] [PartialOrder K] [StarRing K] [StarOrderedRing K]
  (hA : âˆ€ â¦ƒa : Aâ¦„, 0 â‰¤ a â†” âˆƒ b, a = star b * b)
  {F : Type*} [FunLike F A K] [StarHomClass F A K] [MulHomClass F A K]
  (f : F) :
  LinearMap.IsPosMap f :=
by
  intro a ha
  obtain âŸ¨b, rflâŸ© := hA.mp ha
  rw [map_mul, map_star]
  exact star_mul_self_nonneg _

theorem NonUnitalAlgHom.isPosMap_iff_isReal_of_nonUnitalStarAlgEquiv_piMat
  (hÏ† : isEquivToPiMat A)
  (hA : âˆ€ â¦ƒa : Aâ¦„, 0 â‰¤ a â†” âˆƒ b, a = star b * b)
  {K : Type*}
  [Ring K] [StarRing K] [PartialOrder K] [Algebra â„‚ K] [StarOrderedRing K] [StarModule â„‚ K]
  {f : A â†’â‚™â‚[â„‚] K} :
  LinearMap.IsPosMap f â†” LinearMap.IsReal f :=
by
  have : LinearMap.IsPosMap f â†” LinearMap.IsPosMap (f : A â†’â‚—[â„‚] K) := by rfl
  refine âŸ¨Î» h => isReal_of_isPosMap_of_starAlgEquiv_piMat hÏ† (this.mp h), Î» h => ?_âŸ©
  let f' : A â†’â‹†â‚™â‚[â„‚] K := NonUnitalStarAlgHom.mk f h
  exact starMulHom_isPosMap hA f'

theorem Matrix.innerAut.map_zpow {n : Type*} [Fintype n] [DecidableEq n]
  {ğ•œ : Type*} [RCLike ğ•œ] (U : â†¥(Matrix.unitaryGroup n ğ•œ)) (x : Matrix n n ğ•œ) (z : â„¤) :
  (Matrix.innerAut U) x ^ z = (Matrix.innerAut U) (x ^ z) :=
by
  induction z using Int.induction_on
  . exact map_pow U x 0
  . rename_i i _
    exact map_pow U x (â†‘i + 1)
  . rename_i i _
    calc (innerAut U x ^ (-(i:â„¤)-1))
      = (innerAut U x ^ (i + 1))â»Â¹ :=
        by rw [â† Matrix.zpow_neg_natCast, neg_sub_left, add_comm]; rfl
    _ = (innerAut U (x ^ (i + 1)))â»Â¹ := by rw [innerAut.map_pow]
    _ = innerAut U ((x ^ (i + 1))â»Â¹) := by rw [map_inv]
    _ = innerAut U (x ^ (-(i:â„¤)-1)) := by
      rw [â† Matrix.zpow_neg_natCast, neg_sub_left, add_comm]; rfl

lemma Matrix.inv_diagonal' {R n : Type*} [Field R]
  [Fintype n] [DecidableEq n]
  (d : n â†’ R) [Invertible d] :
  (Matrix.diagonal d)â»Â¹ = Matrix.diagonal dâ»Â¹ :=
by
  haveI := Matrix.diagonalInvertible d
  rw [â† invOf_eq_nonsing_inv, invOf_diagonal_eq]
  simp only [diagonal_eq_diagonal_iff, Pi.inv_apply]
  intro
  refine eq_inv_of_mul_eq_one_left ?h
  rw [â† Pi.mul_apply, invOf_mul_self]
  rfl

theorem Matrix.diagonal_zpow
  {ğ•œ : Type*} [Field ğ•œ] (x : n â†’ ğ•œ) [Invertible x] (z : â„¤) :
  (Matrix.diagonal x) ^ z = Matrix.diagonal (x ^ z) :=
by
  induction z using Int.induction_on
  . simp only [zpow_zero]; rfl
  . norm_cast
    exact diagonal_pow x _
  . rename_i i _
    rw [neg_sub_left, add_comm]
    calc diagonal x ^ (-((i : â„¤)+1)) = (diagonal x ^ (-((i + 1 : â„•) : â„¤))) := rfl
      _ = (diagonal x ^ (i + 1))â»Â¹ := by rw [Matrix.zpow_neg_natCast]
      _ = (diagonal (x ^ (i + 1)))â»Â¹ := by rw [diagonal_pow]
      _ = diagonal ((x ^ (i + 1))â»Â¹) := by rw [inv_diagonal']
      _ = diagonal ((x ^ ((i : â„¤) + 1))â»Â¹) := by norm_cast
      _ = diagonal (x ^ (-((i:â„¤)+1))) := by
        rw [â† _root_.zpow_neg, neg_add]

theorem Matrix.PosDef.rpow_zpow {ğ•œ : Type*} [RCLike ğ•œ]
  {Q : Matrix n n ğ•œ} (hQ : Q.PosDef) (r : â„) (z : â„¤) :
  (hQ.rpow r) ^ z = hQ.rpow (r * (z : â„)) :=
by
  have := PosDef.rpow.isPosDef hQ r
  rw [hQ.rpow_eq, innerAut_posDef_iff, Matrix.PosDef.diagonal_iff] at this
  have : Invertible (RCLike.ofReal âˆ˜ (hQ.1.eigenvalues ^ r) : n â†’ ğ•œ) :=
  by
    simp only [Function.comp_apply, Pi.pow_apply, RCLike.ofReal_pos] at this
    use RCLike.ofReal âˆ˜ (hQ.1.eigenvalues ^ (-r))
    <;>
    { ext i
      simp only [Pi.mul_apply, Function.comp_apply, Pi.pow_apply, Pi.one_apply,
        â† RCLike.ofReal_mul]
      rw [â† Real.rpow_add (hQ.eigenvalues_pos _)]
      simp only [neg_add_cancel, add_neg_cancel, Real.rpow_zero, RCLike.ofReal_one] }
  simp_rw [rpow_eq, innerAut.map_zpow, diagonal_zpow]
  congr
  simp only [diagonal_eq_diagonal_iff, Pi.pow_apply, Function.comp_apply,
    â† RCLike.ofReal_zpow, RCLike.ofReal_inj]
  intro
  rw [Real.rpow_mul (le_of_lt (hQ.eigenvalues_pos _))]
  norm_cast

theorem Matrix.PosDef.rpow_eq_pow {ğ•œ : Type*} [RCLike ğ•œ]
  {Q : Matrix n n ğ•œ} (hQ : Q.PosDef) (r : â„•) :
  hQ.rpow r = Q ^ r :=
by
  nth_rw 2 [hQ.1.spectral_theorem'']
  simp only [innerAut.map_pow, diagonal_pow]
  rw [rpow_eq]
  congr
  simp only [diagonal_eq_diagonal_iff, Function.comp_apply, Pi.pow_apply, Real.rpow_natCast,
    RCLike.ofReal_pow, implies_true]
theorem Matrix.PosDef.rpow_eq_zpow {ğ•œ : Type*} [RCLike ğ•œ]
  {Q : Matrix n n ğ•œ} (hQ : Q.PosDef) (r : â„¤) :
  hQ.rpow r = Q ^ r :=
by
  letI := PosDef.eigenvaluesInvertible' hQ
  nth_rw 2 [hQ.1.spectral_theorem'']
  simp only [innerAut.map_zpow, diagonal_zpow]
  rw [rpow_eq]
  congr
  simp only [diagonal_eq_diagonal_iff, Function.comp_apply, Pi.pow_apply, Real.rpow_intCast,
    RCLike.ofReal_zpow, implies_true]

theorem Matrix.PosDef.rpow_rzpow {ğ•œ : Type*} [RCLike ğ•œ]
  {Q : Matrix n n ğ•œ} (hQ : Q.PosDef) (r : â„) (z : â„¤) :
  (PosDef.rpow.isPosDef hQ r).rpow z = hQ.rpow (r * (z : â„)) :=
by rw [rpow_eq_zpow, rpow_zpow]

theorem Matrix.PosDef.eq_zpow_of_zpow_inv_eq {ğ•œ : Type*} [RCLike ğ•œ]
  {Q R : Matrix n n ğ•œ} (hQ : Q.PosDef)
  {z : â„¤} (hz : z â‰  0)
  (h : hQ.rpow (zâ»Â¹) = R) : Q = R ^ z :=
by
  have : (hQ.rpow zâ»Â¹) ^ (z : â„¤) = R ^ (z : â„¤) := by rw [h]
  rw [rpow_zpow, inv_mul_cancelâ‚€ (Int.cast_ne_zero.mpr hz), rpow_one_eq_self] at this
  exact this

theorem Matrix.PosDef.eq_of_zpow_inv_eq_zpow_inv {ğ•œ : Type*} [RCLike ğ•œ]
  {Q R : Matrix n n ğ•œ} (hQ : Q.PosDef) (hR : R.PosDef)
  {r : â„¤} (hr : r â‰  0) (hQR : hQ.rpow râ»Â¹ = hR.rpow râ»Â¹) : Q = R :=
by
  have := eq_zpow_of_zpow_inv_eq hQ hr hQR
  rw [rpow_zpow, inv_mul_cancelâ‚€ (Int.cast_ne_zero.mpr hr),
    rpow_one_eq_self] at this
  exact this

theorem selfAdjointDecomposition_ext_iff
  {B : Type*} [AddCommGroup B] [StarAddMonoid B]
  [Module â„‚ B] [StarModule â„‚ B] (a b : B) :
    a = b â†” aL a = aL b âˆ§ aR a = aR b :=
by
  refine âŸ¨Î» h => by simp [h], Î» h => ?_âŸ©
  rw [selfAdjointDecomposition a, h.1, h.2]
  exact Eq.symm (selfAdjointDecomposition b)

theorem selfAdjointDecomposition_left_of
  {B : Type*} [AddCommGroup B] [StarAddMonoid B]
  [Module â„‚ B] [StarModule â„‚ B] (a b : B)
  (ha : IsSelfAdjoint a) (hb : IsSelfAdjoint b) :
    aL (a + Complex.I â€¢ b) = a :=
by
  rw [selfAdjointDecomposition_left, star_add, star_smul, ha, hb,
    Complex.star_def, Complex.conj_I, neg_smul, add_add_add_comm, add_neg_cancel, add_zero,
    â† two_smul â„‚, smul_smul]
  norm_num

theorem selfAdjointDecomposition_right_of
  {B : Type*} [AddCommGroup B] [StarAddMonoid B]
  [Module â„‚ B] [StarModule â„‚ B] (a b : B)
  (ha : IsSelfAdjoint a) (hb : IsSelfAdjoint b) :
    aR (a + Complex.I â€¢ b) = b :=
by
  rw [selfAdjointDecomposition_right, star_add, star_smul, ha, hb,
    Complex.star_def, Complex.conj_I, neg_smul, sub_eq_add_neg,
    neg_add, add_add_add_comm, add_neg_cancel, zero_add, â† two_smul â„‚]
  simp only [smul_smul, smul_neg]
  simp only [RCLike.I_to_complex, one_div, isUnit_iff_ne_zero, ne_eq, OfNat.ofNat_ne_zero,
    not_false_eq_true, IsUnit.inv_mul_cancel_left, Complex.I_mul_I, neg_smul, one_smul, neg_neg]

theorem complex_decomposition_mul_decomposition
  {B : Type*} [Ring B] [StarRing B]
  [Module â„‚ B] [StarModule â„‚ B] [IsScalarTower â„‚ B B]
  [SMulCommClass â„‚ B B] (a b c d : B) :
    (a + Complex.I â€¢ b) * (c + Complex.I â€¢ d)
      = (a * c - b * d) + Complex.I â€¢ (b * c + a * d) :=
by
  simp only [mul_add, add_mul, sub_eq_add_neg, smul_add]
  simp only [add_assoc]
  congr 1
  simp only [smul_mul_assoc, mul_smul_comm]
  nth_rw 1 [â† add_assoc, add_comm]
  congr 1
  simp only [smul_smul, Complex.I_mul_I, neg_smul, one_smul]

theorem selfAdjointDecomposition_left_mul_self
  {B : Type*} [Ring B] [StarRing B]
  [Module â„‚ B] [StarModule â„‚ B] [IsScalarTower â„‚ B B]
  [SMulCommClass â„‚ B B] (a : B) :
    aL (a * a) = aL a * aL a - aR a * aR a :=
by
  nth_rw 2 [selfAdjointDecomposition a]
  nth_rw 1 [selfAdjointDecomposition a]
  have : (aL a + RCLike.I â€¢ aR a) * (aL a + RCLike.I â€¢ aR a)
   = _ :=
    complex_decomposition_mul_decomposition _ _ _ _
  rw [this, selfAdjointDecomposition_left_of]
  all_goals { simp only [isSelfAdjoint_iff, star_sub, star_mul,
    isSelfAdjoint_iff.mp (selfAdjointDecomposition_left_isSelfAdjoint _),
    isSelfAdjoint_iff.mp (selfAdjointDecomposition_right_isSelfAdjoint _),
    star_add, add_comm] }
theorem selfAdjointDecomposition_right_mul_self
  {B : Type*} [Ring B] [StarRing B]
  [Module â„‚ B] [StarModule â„‚ B] [IsScalarTower â„‚ B B]
  [SMulCommClass â„‚ B B] (a : B) :
    aR (a * a) = aR a * aL a + aL a * aR a :=
by
  nth_rw 2 [selfAdjointDecomposition a]
  nth_rw 1 [selfAdjointDecomposition a]
  have : (aL a + RCLike.I â€¢ aR a) * (aL a + RCLike.I â€¢ aR a)
   = _ :=
    complex_decomposition_mul_decomposition _ _ _ _
  rw [this, selfAdjointDecomposition_right_of]
  all_goals { simp only [isSelfAdjoint_iff, star_sub, star_mul,
    isSelfAdjoint_iff.mp (selfAdjointDecomposition_left_isSelfAdjoint _),
    isSelfAdjoint_iff.mp (selfAdjointDecomposition_right_isSelfAdjoint _),
    star_add, add_comm] }

theorem isStarNormal_iff_selfAdjointDecomposition_commute
  {B : Type*} [Ring B] [StarRing B]
  [Module â„‚ B] [StarModule â„‚ B] [IsScalarTower â„‚ B B]
  [SMulCommClass â„‚ B B] (p : B) :
    IsStarNormal p â†” Commute (aL p) (aR p) :=
by
  let a := aL p
  let b := aR p
  have h : p = a + Complex.I â€¢ b := selfAdjointDecomposition _
  have hâ‚ : p * star p = (a ^ 2 + b ^ 2) + Complex.I â€¢ (b * a - a * b) :=
  by
    rw [h, star_add]
    nth_rw 2 [star_smul]
    rw [Complex.star_def, Complex.conj_I, neg_smul, â† smul_neg,
      complex_decomposition_mul_decomposition,
      selfAdjointDecomposition_left_isSelfAdjoint, selfAdjointDecomposition_right_isSelfAdjoint,
      mul_neg, sub_neg_eq_add, mul_neg, sub_eq_add_neg]
    simp only [pow_two]
    rfl
  have hâ‚‚ : star p * p = (a ^ 2 + b ^ 2) + Complex.I â€¢ (a * b - b * a) :=
  by
    rw [h, star_add]
    nth_rw 2 [star_smul]
    rw [Complex.star_def, Complex.conj_I, neg_smul, â† smul_neg,
      complex_decomposition_mul_decomposition,
      selfAdjointDecomposition_left_isSelfAdjoint, selfAdjointDecomposition_right_isSelfAdjoint,
      neg_mul, sub_neg_eq_add, neg_mul]
    simp_rw [sub_eq_add_neg, pow_two]
    nth_rw 3 [add_comm]
  have hâ‚ƒ :=
    calc IsStarNormal p â†” p * star p = star p * p :=
        by rw [isStarNormal_iff, commute_iff_eq, eq_comm]
    _ â†” Complex.I â€¢ (b * a - a * b) = Complex.I â€¢ (a * b - b * a) :=
        by rw [hâ‚, hâ‚‚, add_left_cancel_iff]
    _ â†” b * a - a * b = a * b - b * a := smul_right_inj Complex.I_ne_zero
    _ â†” (2 : â„‚) â€¢ (b * a) = (2 : â„‚) â€¢ (a * b) :=
        by simp only [two_smul, sub_eq_iff_eq_add, sub_add_eq_add_sub, eq_sub_iff_add_eq]
    _ â†” b * a = a * b := smul_right_inj two_ne_zero
    _ â†” a * b = b * a := eq_comm
  exact hâ‚ƒ

theorem isSelfAdjoint_iff_selfAdjointDecomposition_right_eq_zero
  {B : Type*} [Ring B] [StarRing B]
  [Module â„‚ B] [StarModule â„‚ B] [IsScalarTower â„‚ B B]
  [SMulCommClass â„‚ B B] (p : B) :
    IsSelfAdjoint p â†” aR p = 0 :=
by
  simp only [isSelfAdjoint_iff, RCLike.I_to_complex, isUnit_iff_ne_zero, ne_eq, Complex.I_ne_zero,
    not_false_eq_true, IsUnit.smul_eq_zero, one_div, inv_eq_zero, OfNat.ofNat_ne_zero, sub_eq_zero]

theorem IsIdempotentElem.isSelfAdjoint_iff_isStarNormal
  {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„‚ V]
  (p : V â†’L[â„‚] V) (hp : IsIdempotentElem p) [CompleteSpace V] :
    IsSelfAdjoint p â†” IsStarNormal p :=
by
  refine' âŸ¨Î» h => by rw [isStarNormal_iff, h], Î» h => _âŸ©
  have h : IsStarNormal (1 - p) :=
  by
  { simp only [isStarNormal_iff, commute_iff_eq, star_sub, star_one,
      mul_sub, sub_mul, mul_one, one_mul]
    simp only [sub_eq_add_neg, add_assoc, neg_add, neg_neg]
    rw [(isStarNormal_iff _).mp h]
    rw [â† add_assoc, add_add_add_comm, add_assoc] }
  have := (ContinuousLinearMap.IsStarNormal.norm_eq_adjoint _).mp h
  have :=
    calc p = star p * p â†” âˆ€ x, â€–(p - (star p * p)) xâ€– = 0 :=
        by
          simp only [norm_eq_zero, ContinuousLinearMap.sub_apply, sub_eq_zero]
          rw [@ContinuousLinearMap.ext_iff]
      _ â†” âˆ€ x, â€–(ContinuousLinearMap.adjoint (1 - p)) (p x)â€– = 0 :=
        by
          simp only [â† ContinuousLinearMap.star_eq_adjoint, star_sub, star_one,
            ContinuousLinearMap.sub_apply, ContinuousLinearMap.mul_apply]
          rfl
      _ â†” âˆ€ x, â€–(1 - p) (p x)â€– = 0 := by simp only [this]
      _ â†” âˆ€ x, â€–(p - p * p) xâ€– = 0 := by simp
      _ â†” p - p * p = 0 := by
        simp only [norm_eq_zero, ContinuousLinearMap.ext_iff, ContinuousLinearMap.zero_apply]
      _ â†” IsIdempotentElem p := by simp only [sub_eq_zero, IsIdempotentElem, eq_comm]
  rw [this.mpr hp]
  exact IsSelfAdjoint.star_mul_self _

open scoped InnerProductSpace
theorem LinearMap.IsPositive'.add_ker_eq_inf_ker
  {ğ•œ V : Type*} [RCLike ğ•œ] [NormedAddCommGroup V] [InnerProductSpace ğ•œ V]
  [FiniteDimensional ğ•œ V] {S T : V â†’â‚—[ğ•œ] V} (hS : S.IsPositive') (hT : T.IsPositive') :
    LinearMap.ker (S + T) = LinearMap.ker S âŠ“ LinearMap.ker T :=
by
  ext x
  simp only [LinearMap.mem_ker, LinearMap.add_apply, Submodule.mem_inf]
  refine' âŸ¨Î» h => ?_, Î» h => by rw [h.1, h.2, add_zero]âŸ©
  rw [eq_comm, â† sub_eq_iff_eq_add, eq_comm, zero_sub] at h
  rw [h, neg_eq_zero, and_self]
  have : âŸªx, S xâŸ«_ğ•œ = 0 := eq_of_le_of_le
    (by rw [h, inner_neg_right, neg_le, neg_zero]; exact hT.2 x)
    (hS.2 x)
  obtain âŸ¨f, rflâŸ© := (LinearMap.isPositive'_iff_exists_adjoint_hMul_self _).mp hS
  simp only [Module.End.mul_apply, LinearMap.adjoint_inner_right, inner_self_eq_zero] at this
  simp only [Module.End.mul_apply, this, map_zero, zero_eq_neg] at h
  exact h

theorem mem_unitary_iff_isStarNormal_and_decomposition_left_sq_add_right_sq_eq_one
  {B : Type*} [Ring B] [StarRing B]
  [Module â„‚ B] [StarModule â„‚ B] [IsScalarTower â„‚ B B]
  [SMulCommClass â„‚ B B] (a : B) :
    a âˆˆ unitary B â†” IsStarNormal a âˆ§ (aL a) ^ 2 + (aR a) ^ 2 = 1 :=
by
  have this1 :=
    calc a * star a = (aL a + Complex.I â€¢ aR a) * star (aL a + Complex.I â€¢ aR a) :=
        by simp_rw [â† RCLike.I_to_complex, â† selfAdjointDecomposition]
      _ = (aL a * aL a + aR a * aR a) + Complex.I â€¢ ((aR a * aL a) - (aL a * aR a)) :=
        by
          rw [star_add]
          nth_rw 2 [star_smul]
          rw [Complex.star_def, Complex.conj_I, neg_smul,
            isSelfAdjoint_iff.mp (selfAdjointDecomposition_left_isSelfAdjoint _),
            isSelfAdjoint_iff.mp (selfAdjointDecomposition_right_isSelfAdjoint _),
            â† smul_neg]
          simp_rw [complex_decomposition_mul_decomposition, mul_neg, sub_neg_eq_add,
            sub_eq_add_neg]
      _ = ((aL a) ^ 2 + (aR a) ^ 2) + Complex.I â€¢ (aR a * aL a - aL a * aR a) :=
        by simp only [pow_two, smul_sub, add_sub_assoc]
  rw [unitary.mem_iff]
  constructor
  . intro h
    have : Commute (aL a) (aR a) :=
    by rw [â† isStarNormal_iff_selfAdjointDecomposition_commute, isStarNormal_iff, commute_iff_eq,
      h.1, h.2]
    simp_rw [isStarNormal_iff_selfAdjointDecomposition_commute, this, true_and]
    rw [this1, this, sub_self, smul_zero, add_zero] at h
    exact h.2
  . rintro âŸ¨h1, h2âŸ©
    rw [(isStarNormal_iff _).mp h1, and_self, this1, h2, add_comm]
    apply add_eq_of_eq_sub
    rw [sub_self, smul_eq_zero, sub_eq_zero]
    right
    exact ((isStarNormal_iff_selfAdjointDecomposition_commute _).mp h1).symm

theorem LinearMap.exists_scalar_isometry_iff_preserves_ortho_of_ne_zero
  {ğ•œ V W : Type*} [RCLike ğ•œ]
  [NormedAddCommGroup V] [InnerProductSpace ğ•œ V]
  [NormedAddCommGroup W] [InnerProductSpace ğ•œ W]
  (hV : 0 < Module.finrank ğ•œ V)
  {T : V â†’â‚—[ğ•œ] W} (hT : T â‰  0) :
  (âˆƒ (Î± : ğ•œË£), Isometry ((Î± : ğ•œ) â€¢ T))
  â†”
  âˆ€ x y, âŸªx, yâŸ«_ğ•œ = 0 â†’ âŸªT x, T yâŸ«_ğ•œ = 0 :=
by
  haveI : Nontrivial V := Module.nontrivial_of_finrank_pos hV
  refine' âŸ¨Î» âŸ¨Î±, hâŸ© x y hxy => _, Î» h => _âŸ©
  . have : âŸªT x, T yâŸ«_ğ•œ = 0 â†” âŸª((Î± : ğ•œ) â€¢ T) x, ((Î± : ğ•œ) â€¢ T) yâŸ«_ğ•œ = 0 :=
      by
        simp_rw [LinearMap.smul_apply, inner_smul_right, inner_smul_left,
          â† mul_assoc, RCLike.mul_conj, mul_eq_zero, sq_eq_zero_iff,
          RCLike.ofReal_eq_zero, norm_eq_zero]
        simp only [Units.ne_zero, false_or]
    rw [this, (isometry_iff_inner _).mp h, hxy]
  . haveI : FiniteDimensional ğ•œ V := Module.finite_of_finrank_pos hV
    let e := stdOrthonormalBasis ğ•œ V
    have : âˆ€ i j, âŸªe i + e j, e i - e jâŸ«_ğ•œ = 0 :=
    Î» i j => by
      simp only [inner_add_left, inner_sub_right,
        orthonormal_iff_ite.mp (e.orthonormal)]
      simp only [â†“reduceIte, eq_comm]
      ring
    have h' : âˆ€ i j, âŸªT (e i), T (e j)âŸ«_ğ•œ =
        if i = j then ((â€–T (e i)â€– ^ 2) : ğ•œ) else 0 :=
    Î» i j => by
      split_ifs with h'
      . simp only [h', inner_self_eq_norm_sq_to_K]
      . apply h
        simp only [orthonormal_iff_ite.mp (e.orthonormal), h', reduceIte]
    have this' := Î» i j => h _ _ (this i j)
    simp only [map_add, map_sub, inner_add_left, inner_sub_right, h',
      reduceIte, add_ite, ite_add, eq_comm, ite_sub_ite] at this'
    simp only [sub_self, add_zero, zero_add] at this'
    simp_rw [@eq_comm _ (0 : ğ•œ), ite_eq_iff, and_true, sub_eq_zero] at this'
    let Î± : â„ := â€–T (e âŸ¨0, hVâŸ©)â€–
    simp only [â† RCLike.ofReal_pow, RCLike.ofReal_inj,
      sq_eq_sqâ‚€ (norm_nonneg _) (norm_nonneg _)] at this'
    have hÎ± : âˆ€ i, Î± = â€–T (e i)â€– := Î» i => by
      by_cases hi : i = âŸ¨0, hVâŸ©
      . rw [hi]
      . specialize this' âŸ¨0, hVâŸ© i
        simp only [hi, eq_comm, false_or, not_false_iff, true_and] at this'
        simp only [Î±, this']
    have : âˆ€ x, â€–T xâ€– = Î± * â€–xâ€– :=
    Î» x => by
      simp_rw [hÎ± âŸ¨0, hVâŸ©]
      rw [â† sq_eq_sqâ‚€ (norm_nonneg _) (mul_nonneg (norm_nonneg _) (norm_nonneg _)),
        â† RCLike.ofReal_inj (K := ğ•œ), RCLike.ofReal_pow,
        mul_pow, RCLike.ofReal_mul, RCLike.ofReal_pow, â† hÎ±, RCLike.ofReal_pow]
      rw [â† OrthonormalBasis.sum_repr e x]
      simp_rw [â† inner_self_eq_norm_sq_to_K]
      simp only [map_sum, map_smul, sum_inner, inner_smul_left,
        inner_sum, inner_smul_right, h', mul_ite, mul_zero,
        Finset.sum_ite_eq', Finset.mem_univ, if_true, â† hÎ±]
      simp only [â† mul_assoc, RCLike.mul_conj, orthonormal_iff_ite.mp (e.orthonormal),
        mul_boole, Finset.sum_ite_eq', Finset.mem_univ, if_true]
      simp_rw [â† Finset.sum_mul, mul_comm]
    have hÎ±' : Î± = 0 â†” T = 0 :=
    by
      refine' âŸ¨Î» h => _, Î» h => _âŸ©
      . simp_rw [h, zero_mul, norm_eq_zero] at this
        ext x
        simp only [LinearMap.zero_apply, this]
      . simp only [h, LinearMap.zero_apply, norm_zero, @eq_comm _ (0 : â„),
          mul_eq_zero, norm_eq_zero] at this
        obtain âŸ¨x, hxâŸ© : âˆƒ x : V, x â‰  0 := by exact exists_ne 0
        specialize this x
        simp only [hx, or_false] at this
        exact this
    simp only [hT, iff_false, â† ne_eq] at hÎ±'
    have hÎ±'' : (Î± : ğ•œ) â‰  0 := by simp only [ne_eq, algebraMap.lift_map_eq_zero_iff, hÎ±',
      not_false_iff]
    use ((Units.mk0 Î± hÎ±'')â»Â¹ : ğ•œË£)
    rw [isometry_iff_norm]
    intro x
    simp only [LinearMap.smul_apply, norm_smul, this, norm_inv]
    simp only [Units.val_inv_eq_inv_val, Units.val_mk0, norm_inv, RCLike.norm_ofReal]
    simp only [RCLike.norm_ofReal, abs_of_nonneg (norm_nonneg _),
      â† hÎ±, Î±, â† mul_assoc, inv_mul_cancelâ‚€ hÎ±', one_mul]

theorem LinearMap.exists_scalar_isometry_iff_preserves_ortho
  {ğ•œ V : Type*} [RCLike ğ•œ]
  [NormedAddCommGroup V] [InnerProductSpace ğ•œ V]
  [Module.Finite ğ•œ V]
  {T : V â†’â‚—[ğ•œ] V} :
  (âˆƒ (Î± : ğ•œ) (S : V â†’â‚—áµ¢[ğ•œ] V), T = Î± â€¢ S.toLinearMap)
  â†”
  âˆ€ x y, âŸªx, yâŸ«_ğ•œ = 0 â†’ âŸªT x, T yâŸ«_ğ•œ = 0 :=
by
  refine' âŸ¨Î» âŸ¨Î±, S, hâŸ© x y hxy => _, Î» h => _âŸ©
  . simp only [h, LinearMap.smul_apply, inner_smul_left, inner_smul_right,
      LinearIsometry.coe_toLinearMap, LinearIsometry.inner_map_map, hxy, mul_zero]
  . by_cases hT : T = 0
    . rw [hT]
      use 0, 1
      simp only [zero_smul]
    . by_cases hV : Module.rank ğ•œ V = 0
      . rw [rank_zero_iff_forall_zero] at hV
        simp only [LinearMap.ext_iff, smul_apply, LinearIsometry.coe_toLinearMap,
          hV, implies_true, exists_true_iff_nonempty]
        use 0, 0
        simp only [zero_apply, norm_zero, hV, implies_true]
      . simp only [â† pos_iff_ne_zero] at hV
        rw [rank_pos_iff_nontrivial] at hV
        have : 0 < Module.finrank ğ•œ V := Module.finrank_pos
        obtain âŸ¨Î±, hÎ±âŸ© := (LinearMap.exists_scalar_isometry_iff_preserves_ortho_of_ne_zero this hT).mpr h
        use (Î±â»Â¹ : ğ•œË£), âŸ¨((Î± : ğ•œ) â€¢ T), (isometry_iff_norm _).mp hÎ±âŸ©
        simp only [Units.val_inv_eq_inv_val, ne_eq, Units.ne_zero, not_false_eq_true,
          inv_smul_smulâ‚€]

theorem LinearMap.isSymmetric_adjoint_mul_self'
  {ğ•œ V W : Type*} [RCLike ğ•œ] [NormedAddCommGroup V] [InnerProductSpace ğ•œ V]
  [NormedAddCommGroup W] [InnerProductSpace ğ•œ W]
  [FiniteDimensional ğ•œ V] [FiniteDimensional ğ•œ W]
  (T : V â†’â‚—[ğ•œ] W) :
    IsSymmetric (LinearMap.adjoint T âˆ˜â‚— T) :=
by
  intro x y
  simp only [coe_comp, Function.comp_apply, adjoint_inner_left, adjoint_inner_right]
